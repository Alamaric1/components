REM ' add a <script> tag to a parent document.

CLASS PUBLIC BuiJavaScriptShim
    FIELD PRIVATE BBjString TimerName$
    FIELD PRIVATE BBjString FileName$
    field private BBjTopLevelWindow Wnd!
    
    METHOD PRIVATE BuiJavaScriptShim(BBjString p_timerName$, BBjString p_fileName$, BBjString html$)
        #TimerName$ = p_timerName$
        #FileName$ = p_fileName$
        
        sg! = BBjAPI().getSysGui()
        #Wnd! = cast(BBjTopLevelWindow,sg!.addWindow(sg!.getAvailableContext(),0,0,0,0,"",$01080003$)) 
        hv! = #Wnd!.addHtmlView(101,10,10,10,10,"")
        hv!.setText(html$)
        
    METHODEND

    METHOD PUBLIC STATIC VOID insertJavascript(BBjString js$)
    
        if info(3,6)<>"5" then
            methodret
        fi
    
        REM ' IE8 doesn't allow data URI's for iframe's.  My goal was to
        REM ' put the javascript shim in the data URI of the iframe, but that
        REM ' won't work in IE.  The only solution I can come up with is to
        REM ' put that HTML file in the bbj/htdocs directory, serve it up
        REM ' and then delete it.  Somehow there would have to be a
        REM ' notification that the script ran.
        REM ' The silver lining is that I can put all the JS into the file.

        REM ' Another alternative is to use the BBjServlet, which would be a
        REM ' *whole* lot easier.
        ch = unt
        pid$ = STR(DEC(INFO(3,0)))
        saveprec = tcb(14)
        PRECISION 16
        x = ROUND(RND(0) * 1000000000000000000, 16)
        precision saveprec
        tmpName$ = pid$ + "-" + STR(x)
        
        file$ = "jsshim" + pid$ + ".html"
        fullFileName$ = System.getProperty("basis.BBjHome") + "/htdocs/" + file$
        ERASE fullFileName$,ERR=*NEXT
        STRING fullFileName$
        open(ch)fullFileName$
        js! = js$
        js! = js!.replaceAll("([""'\\])", "\\$1")
        js! = js!.replaceAll("-->", "--"" + "">")
        js! = js!.replaceAll("[\n\r]", " ")
        print(ch)"<html><head></head><body>"
        print(ch)"<script>//<!--"
        print(ch)"var script=""" + js! + """;"
        print(ch)"var body = parent.document.body;"
        print(ch)"var s = parent.document.createElement(""SCRIPT"");"
        print(ch)"var spre = ""//<!--\n(function() {\n"";"
        print(ch)"var spost = ""\n})();\n//-->"";"
        print(ch)"s.text = spre + script + spost;"
        print(ch)"body.appendChild(s);"
        print(ch)"//--></script></body></html>"
        close(ch)
        url$ = "/files/" + file$
        js! =  new BuiJavaScriptShim(tmpName$, fullFileName$,"<iframe style='border-width:0;margin:0;padding:0' border=0 width=0 height=0 src=""" + url$ + """></iframe>")
        BBJAPI().createTimer(tmpName$, 3,js!, "timer") 
    METHODEND

    METHOD PUBLIC VOID timer(BBjTimerEvent p_ev!)
    
        BBJAPI().removeTimer(#TimerName$,err=*next)
        ERASE #FileName$,err=*next
        #Wnd!.destroy()
        
    METHODEND
CLASSEND



wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,250,250,"TEST")
btn! = wnd!.addButton(100,10,10,200,22,"Start")

wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")
btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,"button")

process_events

byebye:bye

button:
REM         js$="console.log('This is a test message'); "
        js$="parent.myFunction('xyz'); "     
        BuiJavaScriptShim.insertJavascript(js$)
return
