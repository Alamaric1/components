use ::translations/bbtranslator.bbj::BBTranslator

class public BrowserChecker

    field private static BBjNumber minIEVersion = 10
    field private static BBjNumber minChromeVersion = -1
    field private static BBjNumber minFireFoxVersion = -1
    field private static BBjNumber minSafariVersion = -1
    
    method public static void checkBrowser(java.lang.Boolean allowContinue!)
        locale$ = BBjAPI().getThinClient().getClientLocale()        
        t! = BBTranslator.getInstance("i18n",locale$, "en" , "components/i18n/" )
        if(#isBrowserToOld()) then
            if(allowContinue!) then 
                a = msgbox(t!.getTranslation("OLD_BROWSER") + t!.getTranslation("DO_YOU_WISH_TO_CONTINUE"),64+1,t!.getTranslation("INFORMATION")) 
                if(a = 1) then
                    methodret
                else
                    release
                fi
            else
                a = msgbox(t!.getTranslation("OLD_BROWSER"),64+0, t!.getTranslation("INFORMATION")) 
                release
            fi
        fi
    methodend

    method public static java.lang.Boolean isBrowserToOld()
        userAgent! = cast(String,cvs(info(3,8),8))
        if(userAgent! = null()) then
            methodret java.lang.Boolean.FALSE
        fi
        
        userAgent! = userAgent!.toUpperCase()
        
        if(userAgent!.contains("MSIE")) then
            browser! = "MSIE"
        fi
        
        if(userAgent!.contains("CHROME")) then
            browser! = "CHROME"
        fi
        
        if(userAgent!.contains("FIREFOX")) then
            browser! = "FIREFOX"
        fi
        
        if(userAgent!.contains("SAFARI")) then
            browser! = "SAFARI"
        fi
        
        if(browser! = null()) then
            methodret java.lang.Boolean.FALSE
        fi
        
        methodret #checkMinVersion(userAgent!,browser!)
    methodend
    
    method private static java.lang.Boolean checkMinVersion(String userAgent!, String browser!)
        userAgent! = userAgent!.toUpperCase()
        
        switch 1
            case browser! = "MSIE"
                position = userAgent!.indexOf("MSIE")
                userAgent! = userAgent!.substring(position, userAgent!.indexOf(";",position))
                version = #getVersion(cvs(userAgent!.substring(userAgent!.indexOf(" ")),3))
                minVersion = #minIEVersion
                break
                
            case browser! = "CHROME"
                position = userAgent!.indexOf("CHROME")
                userAgent! = userAgent!.substring(position, userAgent!.indexOf(" ", position))
                version = #getVersion(cvs(userAgent!.substring(userAgent!.indexOf("/")+1, userAgent!.indexOf("." , userAgent!.indexOf("/"))),3))
                minVersion = #minChromeVersion
                break
            
            case browser! = "FIREFOX"
                position = userAgent!.indexOf("FIREFOX")
                userAgent! = userAgent!.substring(position)
                version = #getVersion(cvs(userAgent!.substring(userAgent!.indexOf("/")+1),3))
                minVersion = #minFireFoxVersion
                break
            
            case browser! = "SAFARI"
                position = userAgent!.lastIndexOf(" ")
                userAgent! = userAgent!.substring(0,position)
                version = #getVersion(cvs(userAgent!.substring(userAgent!.lastIndexOf("/")+1),3))
                minVersion = #minSafariVersion
                break
        swend 
        
        if(minVersion = -1 OR version = -1) then
            methodret java.lang.Boolean.FALSE
        else
            if(version < minVersion) then
                methodret java.lang.Boolean.TRUE
            else
                methodret java.lang.Boolean.FALSE
            fi
        fi  
        
        methodret java.lang.Boolean.FALSE
    methodend
    
    method private static BBjNumber getVersion(BBjString str$)
        seterr VersionConversion
        
          fullVersion! = cast(String,str$)
          
          if(fullVersion!.indexOf(".") = fullVersion!.lastIndexOf(".")) then
              version = num(fullVersion!)
          else
              version = num(fullVersion!.substring(0, fullVersion!.indexOf("." , fullVersion!.indexOf(".") +1)))
          fi
          methodret version
          
        VersionConversion:
          methodret -1
    methodend

classend