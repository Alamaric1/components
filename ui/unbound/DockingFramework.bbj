use ::components/ui/unbound/DockingTabsComponent.bbj::DockingTabsComponent
rem use ::components/ui/bound/UIController.bbj::UIController
use ::components/ui/unbound/UIWidget.bbj::UIWidget

rem /**
rem * FrameSet
rem *
rem *
rem *
rem * @author Stephan Wald
rem * @version 1.0
rem */
class public FrameSet

    field public BBjWindow Wnd!
    field private BBjChildWindow ToolBar!
    field private BBjChildWindow MainFrame!

    field private BBjToolButton TbReset!

    field private BBjListButton LbRestore!
    field private BBjToolButton TbRestore!
    field private BBjToolButton TbSDI!

    field public FrameList FrameList!
    field public FrameList MinimizeList!
    field public FrameList SDIList!

    field private BBjNumber X
    field private BBjNumber Y
    field private BBjNumber W
    field private BBjNumber H

    field private BBjNumber NewWidth
    field private BBjNumber NewHeight

    field private Boolean showToolbar!

    rem    field public BBjColor SliderColor!    = BBjAPI().makeColor("LTGRAY")
    field public static BBjColor SliderColor!       = BBjAPI().makeColor("MAGENTA")


    field private BBjChildWindow Marker!

    method private FrameSet()

        
    methodend

    method public FrameSet(BBjWindow wnd!, BBjNumber id, BBjNumber x, BBjNumber y, BBjNumber w, BBjNumber h)

       

        #MainFrame!=wnd!.addChildWindow(id,x,y,w,h,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())


        #showToolbar! = Boolean.TRUE

        #X=x
        #Y=y
        #W=w
        #H=h

        #initialize()

    methodend

    method public FrameSet(BBjWindow wnd!, BBjNumber id, BBjNumber x, BBjNumber y, BBjNumber w, BBjNumber h, Boolean showToolbar!)

        #MainFrame!=wnd!.addChildWindow(id,x,y,w,h,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())


        #showToolbar! = showToolbar!

        #X=x
        #Y=y
        #W=w
        #H=h

        #initialize()

    methodend


    method private void initialize()

        windowOffsetY = 0

        if #showToolbar! then
            #ToolBar!   = #MainFrame!.addChildWindow(#MainFrame!.getAvailableControlID(),0,0,#W,25,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())

            offs=5

            #TbReset! = #ToolBar!.addToolButton(100,offs,1,22,22,"")
            imgReset! = BBjAPI().getSysGui().getImageManager().loadImageFromBytes($89504E470D0A1A0A0000000D49484452000000100000001008060000001FF3FF61000002614944415478DADD925B48D37114C77F1016B10C32A2C09885925241FA9026F990384DD426166B4104F39A5A6212CE95967F5B3353673357B6875AFDDBC2CDCDB4E6484B7CB0F28290A630ACBC4F8617D46E28D3F9EDB74952AC271F3BF0E17738BFF33D702E84FC7F1653AADFB56E7104A3D91F27D37639FD2829BB9757C4E647326CB3505E6B39AF6A44A2B2C11AC5B0DA48461DE8263EC0E8379E2CD5F7BDEA1F47F40DEDFB3273377AC666605F76E04F9BFEBE806CF68D234CF228EDAF024159AAAAD7C3D3685B5EC1E0E41C8669B2CE0614345B51D8624351EB24C4A621283F2E63746505190F5B70285B15E612FB2529F8570C1DD04E00A99A5E9C28D621D7340A91BA1331C546F0182D7885AC8BF8B27A243FEE8269620EBE299575649BA0841B90513D6B9CB323496341F4CD5A0CD8E671B6B201DAF601586C5FD7DA585872E0F3D40FDC6DEA05BFDC0CEF73F241B2294EDA765C6E4674C53B844B4DD07D1841ED14B048053D9427234049FB375C7AFE0959060BD4438095C6952DFDF04DAD9A22E418B3858417F039B1529DF07E23EAE60111DB8B0BC62FC8793906BEA21501190F667D92154FB9A20A894FA2A22FF296194D8B4BE08AEEFC74CD60AB80F1DA975E3DAE9FB5E362BD15E98611D71B7CB50E9EA764B94450B1796DDA02FD064EBCAC23E15E27769C299B590DF20A6B3809E5E0A6A8E19F5583C3F96644DC7E0BBEB21B24E2BAD06DE7E1D73283F25E80EA9EAD068E8AFD49A838988448D248489E84FA2A7224AF99D24D69732B40F3B6C796DA49A8C46F7D277B3047E1117839F35F5F1E1467BF9E142FCA4E8A37658FF35C2801CEA325BB4FA7FE16FC02B06D59BD18C7847F0000000049454E44AE426082$)
            #TbReset!.setImage(imgReset!)
            #TbReset!.setTabTraversable(0)
            #TbReset!.setCallback(BBjAPI.ON_TOOL_BUTTON_PUSH,#this!,"onTbResetAll")


            offs=offs+32

            #LbRestore! = #ToolBar!.addListButton(102,offs,1,150,220,"")
            #LbRestore!.setEnabled(0)
            #LbRestore!.setMaximumRowCount(500)
            offs=offs+152


            #TbRestore! = #ToolBar!.addToolButton(103,offs,1,22,22,"")
            imgRestore! = BBjAPI().getSysGui().getImageManager().loadImageFromBytes($89504E470D0A1A0A0000000D49484452000000100000001008060000001FF3FF61000001A64944415478DA6360189420A6AEA78B2C8DA1A1AB9893EA27CE8BAEEAF94FBAE6C23ECEB8FA899B37DDFFF83FBCA29B340342B31A78A26B261E5A7FF7D3FFA58FFFFFCF9DBDFD7F5049270C7F00E2C3C1C55D6958357B6575498496F75D5871E3D3FFD9B7FEFFAFDAF9F8FFFDFFFFFFDF83E22BBFFEFDDFFBEEFBFF8EAD97FEFBE6B7AFC030C033A775D7C22B1FFFF75FF8F73F67EDEDFF69CBAFA2E0FC0DF7FEF79CFDFD7FE3A7FFFFDBB75CFDEF96D3128E6280435683844B66F3E5A89E8DFFCB77BEFA9FBAFC06982EDFF112420371CCBCF3FF8BB73EFBDF77F2CB7FA7F4C68318AE304E2BE7B749AA3B1CD8B2FA7FCE8627FFFD1B97FFB74EACFDEF5A38F97FD4A47D60B1E0E9A7FF67AF7DF0DF3EB5F10BD6B0B00C2DE4348DA9DAE85EB5E4BF6974153816CC222B2DED7326FC4F5876FFBF6BF791FF094BEFC1E570A4835066FDF0F2F9FA61E570452076D89C1BFFAD9BF6816964399C402BB0A417C6D60C2CF9EF3DF9F27F83CA9D601AC427298DA8F816FD77EA3EF75FBD700B9806F1493240DE33FFBF65CB293806F171A96504627620E6066201201603626969B7BCFFE818591300309806BC44605D060000000049454E44AE426082$)
            #TbRestore!.setImage(imgRestore!)
            #TbRestore!.setTabTraversable(0)
            #TbRestore!.setCallback(BBjAPI.ON_TOOL_BUTTON_PUSH,#this!,"onTbRestore")
            #TbRestore!.setEnabled(0)

            offs=offs+22

            windowOffsetY = #ToolBar!.getHeight()
        fi



        #Wnd!       = #MainFrame!.addChildWindow(#MainFrame!.getAvailableControlID(),0,windowOffsetY,#W,#H-windowOffsetY,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())

       
        #FrameList! = new FrameList(2,#this!)
        #MinimizeList! = new FrameList(2,#this!)

    methodend

    method public FrameCell addCell(BBjString Title$)


        methodret #addCell(Title$, Boolean.TRUE, Boolean.TRUE)


    methodend

    method public FrameCell addCell(BBjString Title$, Boolean allowTabClose!, Boolean allowTabDrag!)

        cw! = #Wnd!.addChildWindow(#Wnd!.getAvailableControlID(),0,0,10,10,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
        rem st! = cw!.addStaticText(10,10,10,200,20,Title$)

        c! = new FrameCell(Title$,cw!,uic!, allowTabClose!,allowTabDrag!)

        declare FrameList l!
        l! = #getFrameList()

        while l!.getContentType() <> 2
            l!=l!.getItem(0)
        wend

        l!.addItem(c!)

        #FrameList!.render(#Wnd!,0,0,#W,#H)

        methodret c!

    methodend


    method public void notifyDragTab(BBjNumber x, BBjNumber y)

        if #Marker! <> null() then
            #Marker!.destroy()
        fi

        #Marker! = #FrameList!.markDragTarget(x, y, #Wnd!)

    methodend


    method public BBjNumber notifyDropTab(FrameCell frameCell!)

        if #Marker! <> null() then
            ret = #FrameList!.processDrop(frameCell!)
            #Marker!.destroy()
            #Marker! = null()
            #FrameList!.render(#Wnd!,0,0,#W,#H)


            #FrameList!.cleanup()


            rem ch=unt
            rem open (ch,mode="O_CREATE,O_TRUNC")"dockdebug.txt"
            rem #FrameList!.debug(ch,0)
            rem close (ch)

            methodret ret

        fi

        methodret 0

    methodend


    method public void removeFrameCell(FrameCell fc!)

        #FrameList!.removeFrameCell(fc!)

    methodend

    method public void onTbResetAll(BBjToolButtonPushEvent ev!)

        declare FrameList fl!
        fl! = new FrameList(2,#this!)
        #FrameList!.resetAll(fl!)
        #FrameList! = fl!
        #FrameList!.render(#Wnd!,0,0,#W,#H)

    methodend

    method public void onTbRestore(BBjToolButtonPushEvent ev!)

        id = #LbRestore!.getSelectedIndex()

        pane! = #MinimizeList!.getItem(id)
        #MinimizeList!.removeItem(id)
        #updateMinimizeList()

        #FrameList!.addItemLeaf(pane!)
        #FrameList!.render(#Wnd!,0,0,#W,#H)

    methodend




    method public void updateMinimizeList()

        siz=#MinimizeList!.size()

        #LbRestore!.setEnabled(siz)
        #TbRestore!.setEnabled(siz)


        #LbRestore!.removeAllItems()

        if siz then

            for i=0 to siz-1
                pane! = #MinimizeList!.getItem(i)
                #LbRestore!.addItem(pane!.getTitle())
            next


        fi

    methodend


    method public void setSize(BBjNumber w, BBjNumber h)

        BBjAPI().removeTimer("DELAYEDRESIZE",err=*next)

        #NewWidth = w
        #NewHeight = h

        BBjAPI().createTimer("DELAYEDRESIZE",1,#this!,"delayedResize")


    methodend

    method public void delayedResize(BBjTimerEvent ev!)


        BBjAPI().removeTimer("DELAYEDRESIZE",err=*next)

        #MainFrame!.setSize(#NewWidth,#NewHeight)
        #Wnd!.setSize(#NewWidth,#NewHeight-25)

        dw=#NewWidth-#W
        dh=#NewHeight-#H

        #FrameList!.resizeRight(dw)
        #FrameList!.resizeBottom(dh)

        #W = #NewWidth
        #H = #NewHeight


    methodend

classend



interface public FrameElement

    method public BBjString getTitle()

interfaceend



rem /**
rem * FrameCell
rem *
rem *
rem *
rem * @author Stephan Wald
rem * @version 1.0
rem */
class public FrameCell implements FrameElement

    field public BBjString Title$
    field public BBjChildWindow Wnd!
    field public UIWidget UIWidget!

    field private BBjNumber X
    field private BBjNumber Y
    field private BBjNumber W
    field private BBjNumber H

    field private Boolean showClose!
    field private Boolean allowTabDrag!

    method private FrameCell()


    methodend


    method public FrameCell(BBjString Title$, BBjChildWindow wnd!, UIWidget widget!)

        #Title$ = Title$
        #Wnd! = wnd!
        #UIWidget! = widget!
        #showClose! = Boolean.TRUE
        #allowTabDrag! = Boolean.TRUE

    methodend

    method public FrameCell(BBjString Title$, BBjChildWindow wnd!, UIWidget widget!, Boolean showClose!, Boolean allowTabDrag!)

        #Title$ = Title$
        #Wnd! = wnd!
        #UIWidget! = widget!
        #showClose! = showClose!
        #allowTabDrag! = allowTabDrag!

    methodend

    method public void setVisible(BBjNumber vis)

        #Wnd!.setVisible(vis)

        if #UIWidget! <>null() then
            #UIWidget!.setVisible(vis)
        fi

    methodend

    method public void setLocation(BBjNumber x, BBjNumber y)

        #Wnd!.setLocation(x+10,y+10)

        if #UIWidget! <> null() then
            #UIWidget!.setLocation(x+10,y+10)
        fi

        #X=x
        #Y=y

        #Wnd!.getControl(10,err=*next).setText(#Title$+" X="+str(#X)+" Y="+str(#Y)+" W="+str(#W)+" H="+str(#H))

    methodend


    method public void setSize(BBjNumber w, BBjNumber h)

        #Wnd!.setSize(w-20,h-20)

        if #UIWidget! <> null() then
            #UIWidget!.setSize(w-20,h-20)
        fi


        #W=w
        #H=h

        #Wnd!.getControl(10,err=*next).setText(#Title$+" X="+str(#X)+" Y="+str(#Y)+" W="+str(#W)+" H="+str(#H))

    methodend

    method public BBjNumber getWidth()

        methodret #W

    methodend

    method public BBjNumber getHeight()

        methodret #H

    methodend

    method public BBjNumber getX()

        methodret #X

    methodend

    method public BBjNumber getY()

        methodret #Y

    methodend

    method public Boolean isShowClose()

        methodret #showClose!

    methodend

    method public Boolean isAllowTabDrag()

        methodret #allowTabDrag!

    methodend

classend

rem /**
rem * FrameList
rem *
rem *
rem *
rem * @author Stephan Wald
rem * @version 1.0
rem */
class public FrameList implements FrameElement

    field private BBjVector Contents!
    field public BBjNumber ContentType
    rem 0=Columns, 1=Rows, 2=Cell with Tabs


    field private BBjNumber X
    field private BBjNumber Y
    field private BBjNumber W
    field private BBjNumber H


    field public FrameSet FrameSet!

    field private DockingTabsComponent Tabs!
    field private BBjNumber Split
    field private BBjVector SplitterPaneChildWindows!
    field private BBjNumber SplitterPaneDragging
    field private BBjNumber SplitterOffsX
    field private BBjNumber SplitterOffsY

    field public static BBjImage ImgSliderVertical! = BBjAPI().getSysGui().getImageManager().loadImageFromBytes($FFD8FFE000104A46494600010101006000600000FFDB00430006040506050406060506070706080A100A0A09090A140E0F0C1017141818171416161A1D251F1A1B231C1616202C20232627292A29191F2D302D283025282928FFDB0043010707070A080A130A0A13281A161A2828282828282828282828282828282828282828282828282828282828282828282828282828282828282828282828282828FFC00011080001000703012200021101031101FFC4001500010100000000000000000000000000000004FFC4001A1001000105000000000000000000000000000306083673B3FFC4001501010100000000000000000000000000000405FFC4001B110002010500000000000000000000000000017104053233D1FFDA000C03010002110311003F00A6E1308A6F4C3C630172D7A9CF00D566A0FFD9$)
    field public static BBjImage ImgSliderHorizontal! = BBjAPI().getSysGui().getImageManager().loadImageFromBytes($FFD8FFE000104A46494600010001006000600000FFFE001F4C45414420546563686E6F6C6F6769657320496E632E2056312E303100FFDB0084000505050805080C07070C0C0909090C0D0C0C0C0C0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D010508080A070A0C07070C0D0C0A0C0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0D0DFFC401A20000010501010101010100000000000000000102030405060708090A0B0100030101010101010101010000000000000102030405060708090A0B100002010303020403050504040000017D01020300041105122131410613516107227114328191A1082342B1C11552D1F02433627282090A161718191A25262728292A3435363738393A434445464748494A535455565758595A636465666768696A737475767778797A838485868788898A92939495969798999AA2A3A4A5A6A7A8A9AAB2B3B4B5B6B7B8B9BAC2C3C4C5C6C7C8C9CAD2D3D4D5D6D7D8D9DAE1E2E3E4E5E6E7E8E9EAF1F2F3F4F5F6F7F8F9FA1100020102040403040705040400010277000102031104052131061241510761711322328108144291A1B1C109233352F0156272D10A162434E125F11718191A262728292A35363738393A434445464748494A535455565758595A636465666768696A737475767778797A82838485868788898A92939495969798999AA2A3A4A5A6A7A8A9AAB2B3B4B5B6B7B8B9BAC2C3C4C5C6C7C8C9CAD2D3D4D5D6D7D8D9DAE2E3E4E5E6E7E8E9EAF2F3F4F5F6F7F8F9FAFFC00011080007000103011100021101031101FFDA000C03010002110311003F00EC3FE13987FE80D17FDF163FFC452B3EE51FFFD9$)



    method private FrameList()

    methodend

    method public FrameList(BBjNumber type, FrameSet fs!)

        #Contents! = new BBjVector()
        #SplitterPaneChildWindows! = new BBjVector()
        #ContentType = type
        #FrameSet! = fs!

    methodend

    method public BBjNumber getWidth()


        methodret #W


    methodend

    method public BBjNumber getHeight()


        methodret #H


    methodend

    method public BBjNumber getX()


        methodret #X


    methodend

    method public BBjNumber getY()


        methodret #Y


    methodend

    method public void setSize(BBjNumber w, BBjNumber h)

        #H = h
        #W = w

    methodend

    method public BBjNumber size()


        methodret #Contents!.size()


    methodend


    method public void setLocation(BBjNumber x, BBjNumber y)

        #X=x
        #Y=y

    methodend


    method public FrameElement getItem(BBjNumber i)

        fe! = #Contents!.getItem(i)

        methodret fe!

    methodend

    method public void addItemLeaf(FrameElement el!)



        if #ContentType = 2 or #size()=0 then
            #Contents!.addItem(el!)
        else
            el1! = #Contents!.getItem(0)
            el1!.addItem(el!)
        fi

    methodend


    method public void addItem(FrameElement el!)

        #Contents!.addItem(el!)

    methodend

    method public void removeItem(BBjNumber i)

        if #ContentType = 2 then
            el! = #getItem(i)
            el!.getWnd().setVisible(0)
        fi

        #Contents!.removeItem(i)

    methodend

    method public void clear()

        #Contents!.clear()

    methodend


    method public void resetAll(FrameList fl!)


        if #Tabs! <> null() then
            #Tabs!.destroy()
            #Tabs! = null()
        fi

        #this!.removeSplitterPaneChildWindows()

        if #size() then

            for i=0 to #size()-1

                if #ContentType=2 then
                    fl!.addItem(#getItem(i))
                else
                    e! = #getItem(i)
                    e!.resetAll(fl!)
                fi

            next

        fi

    methodend

    method public void notifySelectionChange(BBjNumber id)

        if #size() then

            for i=0 to #size()-1
                cell! = #getItem(i)
                cell!.setVisible(i= #Tabs!.getSelectedTabId())
            next

        fi

    methodend

    method public void removeFrameCell(FrameCell fc!)


        c=#size()

        if c=0 then methodret

        if #ContentType = 2 then

            for i=0 to c-1
                f! = #getItem(i)

                if f! = fc! then
                    #removeItem(i)

                    if #size() = 0 then
                        #Tabs!.destroy()
                        #Tabs! = null()
                    fi


                    methodret

                fi

            next


        else

            for i=0 to c-1
                f! = #getItem(i)
                f!.removeFrameCell(fc!)
            next

        fi


    methodend

    method public void onSplitterPaneChildWindowMouseDown(BBjMouseDownEvent ev!)

        #SplitterPaneDragging = 1
        #SplitterOffsX = ev!.getChildWindow().getX()
        #SplitterOffsY = ev!.getChildWindow().getY()

    methodend

    method public void renderTabs()

        if #Tabs! <> null() then
            #Tabs!.destroy()
            #Tabs! = null()
        fi

        #Tabs! = new DockingTabsComponent(#X+4,#Y+4,#W-4,32,#this!, #FrameSet!)


        if #size() then

            for i=0 to #size()-1
                cell! = #getItem(i)
                #Tabs!.addTab(cell!)
                cell!.setVisible(i= #Tabs!.getSelectedTabId())
                cell!.setLocation(#X,#Y+29)
                cell!.setSize(#W,#H-29)
            next

        fi

    methodend

    method public void resizeLeft(BBjNumber size)

        #X = #X-size
        #W = #W+size


        if #ContentType = 0 then
            pane! = #getItem(0)
            pane!.resizeLeft(size)
        fi

        if #ContentType = 1 then

            for i=0 to #size()-1
                pane! = #getItem(i)
                pane!.resizeLeft(size)

                if i>0 and i<=#SplitterPaneChildWindows!.size() then
                    cw! = #SplitterPaneChildWindows!.getItem(i-1)
                    cw!.setLocation(cw!.getX()-size,cw!.getY())
                    w=cw!.getWidth()+size
                    h=cw!.getHeight()
                    cw!.setSize(w,h)
                    cw!.getControl(10).setSize(w,h)
                fi

            next

        fi


        if #ContentType = 2 then
            #renderTabs()
        fi

    methodend


    method public void resizeRight(BBjNumber size)

        #W=#W+size

        if #ContentType = 0 then
            pane! = #getItem(#size()-1)
            pane!.resizeRight(size)
        fi

        if #ContentType = 1 then

            for i=0 to #size()-1
                pane! = #getItem(i)
                pane!.resizeRight(size)

                if i>0 and i<=#SplitterPaneChildWindows!.size() then
                    cw! = #SplitterPaneChildWindows!.getItem(i-1)
                    cw!.setLocation(cw!.getX(),cw!.getY())
                    w=cw!.getWidth()+size
                    h=cw!.getHeight()
                    cw!.setSize(w,h)
                    cw!.getControl(10).setSize(w,h)
                fi

            next

        fi

        if #ContentType = 2 then
            #renderTabs()
        fi


    methodend

    method public void resizeTop(BBjNumber size)


        #Y = #Y-size
        #H = #H+size



        if #ContentType = 1 then
            pane! = #getItem(0)
            pane!.resizeTop(size)
        fi

        if #ContentType = 0 then

            for i=0 to #size()-1
                pane! = #getItem(i)
                pane!.resizeTop(size)

                if i>0 and i<=#SplitterPaneChildWindows!.size() then
                    cw! = #SplitterPaneChildWindows!.getItem(i-1)
                    cw!.setLocation(cw!.getX(),cw!.getY()-size)
                    w=cw!.getWidth()
                    h=cw!.getHeight()+size
                    cw!.setSize(w,h)
                    cw!.getControl(10).setSize(w,h)
                fi

            next

        fi


        if #ContentType = 2 then
            #renderTabs()
        fi

    methodend

    method public void resizeBottom(BBjNumber size)

        #H=#H+size

        if #ContentType = 1 then
            pane! = #getItem(#size()-1)
            pane!.resizeBottom(size)
        fi

        if #ContentType = 0 then

            for i=0 to #size()-1
                pane! = #getItem(i)
                pane!.resizeBottom(size)

                if i>0 and i<=#SplitterPaneChildWindows!.size() then
                    cw! = #SplitterPaneChildWindows!.getItem(i-1)
                    cw!.setLocation(cw!.getX(),cw!.getY())
                    w=cw!.getWidth()
                    h=cw!.getHeight()+size
                    cw!.setSize(w,h)
                    cw!.getControl(10).setSize(w,h)

                fi

            next

        fi

        if #ContentType = 2 then
            #renderTabs()
        fi


    methodend

    method public void onSplitterPaneChildWindowMouseUp(BBjMouseUpEvent ev!)

        if #SplitterPaneDragging then

            if #ContentType = 0 then
                cw!=ev!.getChildWindow()

                i=0

                while i<#SplitterPaneChildWindows!.size()

                    if #SplitterPaneChildWindows!.getItem(i) = ev!.getChildWindow() then
                        m=i
                        break
                    fi

                    i=i+1
                wend



                left! = #getItem(m)
                right! = #getItem(m+1)


                if left!.getWidth()+ev!.getX() >20 then

                    move = ev!.getX()
                    left!.resizeRight(move)
                    right!.resizeLeft(move*-1)

                    rem width = left!.getWidth()+right!.getWidth()
                    rem slider = ev!.getX()+#SplitterOffsX-left!.getX()


                    rem left!.render(wnd!,left!.getX(), left!.getY(),slider, left!.getHeight())
                    rem right!.render(wnd!,left!.getX()+slider, left!.getY(),width-slider, left!.getHeight())

                    cw!.setLocation(ev!.getX()+#SplitterOffsX, #SplitterOffsY)
                fi

            fi

            if #ContentType = 1 then
                cw!=ev!.getChildWindow()


                i=0

                while i<#SplitterPaneChildWindows!.size()

                    if #SplitterPaneChildWindows!.getItem(i) = ev!.getChildWindow() then
                        m=i
                        break
                    fi

                    i=i+1
                wend

                top! = #getItem(m)
                bottom! = #getItem(m+1)

                move = ev!.getY()
                top!.resizeBottom(move)
                bottom!.resizeTop(move*-1)


                rem height = top!.getHeight()+bottom!.getHeight()
                rem slider = ev!.getY()+#SplitterOffsY-top!.getY()

                rem top!.render(wnd!,top!.getX(), top!.getY(),top!.getWidth(), slider)
                rem bottom!.render(wnd!,top!.getX(), top!.getY()+slider,top!.getWidth(), height-slider)

                cw!.setLocation(#SplitterOffsX,ev!.getY()+#SplitterOffsY)

            fi



        fi

        #SplitterPaneDragging = 0

    methodend


    method public void onSplitterPaneChildWindowMouseMove(BBjMouseMoveEvent ev!)


    methodend


    method public void removeSplitterPaneChildWindows()

        i=0

        while i<#SplitterPaneChildWindows!.size()
            cw! = #SplitterPaneChildWindows!.getItem(i)
            cw!.destroy()
            cw! = null()
            i=i+1
        wend



        #SplitterPaneChildWindows!.clear()


    methodend


    method public void cleanup()



        if #ContentType <> 2 then
            i=0

            while i<#Contents!.size()
                el! = #Contents!.getItem(i)
                el!.cleanup()

                if el!.size()=0 then
                    #removeItem(i)
                    continue

                fi

                i=i+1

            wend

        fi


        if #ContentType <> 2 then

            #removeSplitterPaneChildWindows()

            if  #Contents!.size() then
                declare java.util.TreeMap tm!
                tm! = new java.util.TreeMap()


                for i=0 to #Contents!.size()-1
                    el1! = #Contents!.getItem(i)
                    index = (el1!.getX()+el1!.getY())*100+i
                    tm!.put(index,el1!)
                next

                #Contents!.clear()

                el=0

                if #ContentType=0 then
                    offs=#getX()
                else
                    offs=#getY()
                fi


                wnd! = #FrameSet!.getWnd()

                ks! = tm!.keySet()
                it! = ks!.iterator()

                while it!.hasNext()

                    e! = tm!.get(it!.next())

                    if #ContentType=0 then
                        e!.setLocation(offs,e!.getY())
                        offs=offs+e!.getWidth()

                        if el = tm!.size()-1 then
                            e!.resizeRight(#X + #W - e!.getX()   - e!.getWidth())
                        fi

                        if el then


                            cw! = wnd!.addChildWindow(wnd!.getAvailableControlID(),e!.getX(),e!.getY(),7,e!.getHeight(),$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
                            cw!.setCursor(32644)
                            cw!.setBackColor(#FrameSet!.getSliderColor())
                            i! = cw!.addImageCtrl(10,0,0,7,e!.getHeight(),#ImgSliderVertical!)
                            cw!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onSplitterPaneChildWindowMouseDown")
                            cw!.setCallback(BBjAPI.ON_MOUSE_UP,#this!,"onSplitterPaneChildWindowMouseUp")


                            rem cw!.setCallback(BBjAPI.ON_MOUSE_MOVE,#this!,"onSplitterPaneChildWindowMouseMove")
                            #SplitterPaneChildWindows!.addItem(cw!)


                        fi

                    fi

                    if #ContentType=1 then
                        e!.setLocation(e!.getX(),offs)
                        offs=offs+e!.getHeight()


                        if el = tm!.size()-1 then
                            e!.resizeBottom(#Y + #H - e!.getY()   - e!.getHeight())
                        fi


                        if el then
                            cw! = wnd!.addChildWindow(wnd!.getAvailableControlID(),e!.getX(),e!.getY(),e!.getWidth(),7,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
                            cw!.setCursor(32645)
                            cw!.setBackColor(#FrameSet!.getSliderColor())
                            i! = cw!.addImageCtrl(10,0,0,e!.getW(),7,#ImgSliderHorizontal!)
                            cw!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onSplitterPaneChildWindowMouseDown")
                            cw!.setCallback(BBjAPI.ON_MOUSE_UP,#this!,"onSplitterPaneChildWindowMouseUp")
                            rem cw!.setCallback(BBjAPI.ON_MOUSE_MOVE,#this!,"onSplitterPaneChildWindowMouseMove")
                            #SplitterPaneChildWindows!.addItem(cw!)
                        fi


                    fi


                    #Contents!.addItem(e!)

                    el=el+1
                wend

            fi

        fi

    methodend


    method public void debug(BBjNumber ch,BBjNumber level)

        if #ContentType <> 2 then

            print (ch)fill(level,"*"),

            if #ContentType=0 then print (ch)"Row ",

            if #ContentType=1 then print (ch)"Column ",
            print (ch)"with ",#size()," Elements "
            print (ch)fill(level+1),"Boundaries: X=",#X," Y=",#Y," W=",#W," H=",#H

            for i=0 to #size()-1
                el! = #getItem(i)
                print (ch)fill(level+1),"Element "+str(i)+": X=",el!.getX()," Y=",el!.getY()," W=",el!.getWidth()," H=",el!.getHeight()
                el!.debug(ch,level+3)
            next

        else
            print (ch)fill(level,"*"),"Cell with ",#size()," Elements"
            print (ch)fill(level+1),"Boundaries: X=",#X," Y=",#Y," W=",#W," H=",#H
        fi


    methodend

    method public void render(BBjWindow wnd!, BBjNumber x, BBjNumber y, BBjNumber w, BBjNumber h)


        #X=x
        #Y=y
        #W=w
        #H=h

        #removeSplitterPaneChildWindows()


        if #ContentType <> 2 then
            i=0

            while i<#size()
                pane! = #getItem(i)

                if pane!.size()=0 then
                    pane!.removeSplitterPaneChildWindows()

                    if i=0 then
                        rem "left one went away

                        right! = #getItem(i+1)

                        if #ContentType = 0 then
                            right!.resizeLeft(pane!.getWidth())
                        fi


                        if #ContentType = 1 then
                            right!.resizeTop(pane!.getHeight())
                        fi

                        right!.setLocation(pane!.getX(),pane!.getY())
                    else
                        rem "right one went away"

                        left! = #getItem(i-1)

                        if #ContentType = 0 then
                            left!.resizeRight(pane!.getWidth())
                        fi


                        if #ContentType = 1 then
                            left!.resizeBottom(pane!.getHeight())
                        fi

                    fi

                    #removeItem(i)

                    continue
                fi

                if pane!.size()=1 and pane!.getContentType() <> 2 then
                    pane!.removeSplitterPaneChildWindows()
                    #addItem(pane!.getItem(0))
                    #removeItem(i)
                    continue
                fi

                if pane!.getContentType() = #ContentType then
                    pane!.removeSplitterPaneChildWindows()

                    for j=0 to pane!.size()-1
                        #addItem(pane!.getItem(j))
                    next

                    #removeItem(i)

                fi

                i=i+1

            wend

        fi




        if #ContentType = 0 then
            cols=#size()


            if cols then

                for i=0 to cols-1

                    pane! = #getItem(i)
                    rem pane!.render(wnd!,x+i*int(w/cols),y,int(w/cols),h)
                    pane!.render(wnd!,pane!.getX(), pane!.getY(), pane!.getWidth(), pane!.getHeight())

                    if i then
                        cw! = wnd!.addChildWindow(wnd!.getAvailableControlID(),pane!.getX(),y,7,h,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
                        cw!.setCursor(32644)
                        cw!.setBackColor(#FrameSet!.getSliderColor())
                        i! = cw!.addImageCtrl(10,0,0,7,h,#ImgSliderVertical!)
                        cw!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onSplitterPaneChildWindowMouseDown")
                        cw!.setCallback(BBjAPI.ON_MOUSE_UP,#this!,"onSplitterPaneChildWindowMouseUp")
                        rem cw!.setCallback(BBjAPI.ON_MOUSE_MOVE,#this!,"onSplitterPaneChildWindowMouseMove")
                        #SplitterPaneChildWindows!.addItem(cw!)
                    fi

                next

            fi

        fi

        if #ContentType = 1 then
            rem rows

            rows=#size()


            if rows then

                for i=0 to rows-1
                    pane! = #getItem(i)
                    pane!.render(wnd!,pane!.getX(), pane!.getY(), pane!.getWidth(), pane!.getHeight())
                    rem pane!.render(wnd!,x, y+i*int(h/rows),w,int(h/rows))

                    if i then
                        cw! = wnd!.addChildWindow(wnd!.getAvailableControlID(),x,pane!.getY(),w,7,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
                        cw!.setCursor(32645)
                        cw!.setBackColor(#FrameSet!.getSliderColor())
                        i! = cw!.addImageCtrl(10,0,0,w,7,#ImgSliderHorizontal!)
                        cw!.setCallback(BBjAPI.ON_MOUSE_DOWN,#this!,"onSplitterPaneChildWindowMouseDown")
                        cw!.setCallback(BBjAPI.ON_MOUSE_UP,#this!,"onSplitterPaneChildWindowMouseUp")
                        rem cw!.setCallback(BBjAPI.ON_MOUSE_MOVE,#this!,"onSplitterPaneChildWindowMouseMove")
                        #SplitterPaneChildWindows!.addItem(cw!)
                    fi

                next

            fi


        fi




        if #ContentType = 2 then
            rem tabbed pane



            if #Tabs! <> null() then
                #Tabs!.destroy()
                #Tabs! = null()
            fi

            #Tabs! = new DockingTabsComponent(x+4,y+4,w-4,35,#this!, #FrameSet!)



            if #size() then
                for i=0 to #size()-1
                    cell! = #getItem(i)
                    #Tabs!.addTab(cell!)
                    cell!.setVisible(i= #Tabs!.getSelectedTabId())
                    cell!.setLocation(x,y+29)
                    if cell!.getWidth() <> w or cell!.getHeight()<>h-29 then
                        cell!.setSize(w,h-29)
                    fi
                next

            fi

        fi

    methodend

    method public void closeTab(BBjNumber id)

        #FrameSet!.getMinimizeList().addItem(#getItem(id))
        #FrameSet!.updateMinimizeList()


        #removeItem(id)
        #renderTabs()


        #FrameSet!.getFrameList().cleanup()



    methodend

    method public BBjChildWindow markDragTarget(BBjNumber x, BBjNumber y, BBjWindow wnd!)

        if #ContentType = 2 then

            #Split=0

            if x>#X+#W/5*4 and x<#X+#W and y>#Y and y<#Y+#H then

                marker! = wnd!.addChildWindow(wnd!.getAvailableControlID(),#X+int(#W/2),#Y,int(#W/2),#H,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
                marker!.setVisible(1)
                marker!.setBackColor(BBjAPI().makeColor("LTGRAY"))
                marker!.setOpaque(0)
                #Split=1

                methodret marker!

            fi

            if y>#Y+#H/2 and y<#Y+#H and x>#X and x<#X+#W then

                marker! = wnd!.addChildWindow(wnd!.getAvailableControlID(),#X,#Y+int(#H/2),#W,int(#H/2),$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
                marker!.setVisible(1)
                marker!.setBackColor(BBjAPI().makeColor("LTGRAY"))
                marker!.setOpaque(0)
                #Split=2

                methodret marker!

            fi

            if y>#Y+45 and y<#Y+#H/2 and x>#X and x<#X+#W then

                marker! = wnd!.addChildWindow(wnd!.getAvailableControlID(),#X,#Y+35,#W,int(#H)-35,$$,$00000800$,BBjAPI().getSysGui().getAvailableContext())
                marker!.setVisible(1)
                marker!.setBackColor(BBjAPI().makeColor("LTGRAY"))
                marker!.setOpaque(1)
                #Split=3

                methodret marker!

            fi

        else

            if #size() then

                for i=0 to #size()-1
                    fl! = #getItem(i)
                    ret!= fl!.markDragTarget(x,y,wnd!)

                    if ret!<> null() then

                        methodret ret!

                    fi

                next

            fi

        fi



        methodret null()

    methodend

    method public BBjNumber processDrop(FrameCell frameCell!)

        if #size()=1 then

            for i=0 to #size()-1
                f! = #getItem(i)

                if f! = frameCell! then

                    methodret 0

                fi

            next

        fi

        if #ContentType = 2 then

            if #Split=1 then
                rem two new cols

                #FrameSet!.removeFrameCell(frameCell!)

                left! = new FrameList(2,#FrameSet!)

                if #size() then

                    for i=0 to #size()-1
                        el!=#getItem(i)
                        el!.setSize(int(el!.getWidth()/2), el!.getHeight())
                        left!.addItem(el!)
                    next

                fi

                left!.setSize(int(#W/2),#H)
                left!.setLocation(#X,#Y)


                right! = new FrameList(2,#FrameSet!)
                right!.addItem(frameCell!)

                right!.setLocation(#X+int(#W/2),#Y)
                right!.setSize(int(#W/2),#H)

                #ContentType=0
                #clear()
                #addItem(left!)
                #addItem(right!)


                if #Tabs! <> null() then
                    #Tabs!.destroy()
                    #Tabs! = null()
                fi

                methodret 1

            fi

            if #Split=2 then
                rem two new rows

                #FrameSet!.removeFrameCell(frameCell!)

                top! = new FrameList(2,#FrameSet!)

                if #size() then

                    for i=0 to #size()-1
                        el!=#getItem(i)
                        el!.setSize(el!.getWidth(), int(el!.getHeight()/2))
                        top!.addItem(el!)
                    next

                fi

                top!.setSize(#W,int(#H/2))
                top!.setLocation(#X,#Y)


                bottom! = new FrameList(2,#FrameSet!)
                bottom!.addItem(frameCell!)

                bottom!.setLocation(#X,#Y+int(#H/2))
                bottom!.setSize(#W,int(#H/2))

                #ContentType=1
                #clear()
                #addItem(top!)
                #addItem(bottom!)



                if #Tabs! <> null() then
                    #Tabs!.destroy()
                    #Tabs! = null()
                fi

                methodret 1

            fi

            if #Split=3 then
                rem add to existing tabs

                rem check if tab is already in this list

                c=#size()

                for i=0 to c-1
                    f! = #getItem(i)

                    if f! = frameCell! then

                        methodret 1

                    fi

                next



                #FrameSet!.removeFrameCell(frameCell!)
                #addItem(frameCell!)

                if #Tabs! <> null() then
                    #Tabs!.destroy()
                    #Tabs! = null()
                fi

                methodret 1

            fi


        else

            if #size() then

                for i=0 to #size()-1
                    fl! = #getItem(i)
                    ret= fl!.processDrop(frameCell!)

                    if ret>0 then

                        methodret ret

                    fi

                next

            fi

        fi

        methodret 0

    methodend

classend
















