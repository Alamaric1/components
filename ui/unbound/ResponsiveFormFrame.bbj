chdir "../../../"
use ::components/ui/unbound/UIWidget.bbj::UIWidget
use ::components/ui/unbound/TestWidget.bbj::TestWidget



class public ResponsiveFormFrame

    field private BBjNumber X
    field private BBjNumber Y
    field private BBjNumber W
    field private BBjNumber H
    field private BBjChildWindow CwCanvas!
    field private BBjVector WidgetList!=new BBjVector()
    
    field private BBjNumber MaxWidgetHeight
    field private BBjNumber TotalWidgetHeight
    
    field private BBjNumber Cols=5
    field private BBjNumber CurCols

    method private ResponsiveFormFrame()
    methodend
    
    method public ResponsiveFormFrame(BBjWindow wnd!, BBjNumber id,BBjNumber x, BBjNumber y,BBjNumber w, BBjNumber h)
        #X=x
        #Y=Y        
        #W=w
        #H=h   
        #CwCanvas! = wnd!.addChildWindow(id,#X,#Y,#W,#H,"",$00000800$,BBjAPI().getSysGui().getAvailableContext())
        #CwCanvas!.setBackColor(BBjAPI().makeColor("YELLOW"))
    methodend
    
    method public void setLocation(BBjNumber x, BBjNumber y)
        #X=x
        #Y=Y
        #CwCanvas!.setLocation(x,y)
    methodend
    
    method public void setSize(BBjNumber w, BBjNumber h)
    
        if #W<>w then
            update=1
        fi
        #W=w
        #H=h
        #CwCanvas!.setSize(w,h)
        if update then
            #updateUI()
        fi
        
    methodend
    
    method public BBjNumber getX()
        methodret #X
    methodend
    
    method public BBjNumber getY()
        methodret #Y
    methodend
    
    method public BBjNumber getWidth()
        methodret #W
    methodend
    
    method public BBjNumber getHeight()
        methodret #H
    methodend
    
    method public BBjChildWindow getWnd()
        methodret #CwCanvas!
    methodend    

    method public void addWidget(UIWidget uiw!)
        #WidgetList!.addItem(uiw!)
        #MaxWidgetHeight    = max(#MaxWidgetHeight,uiw!.getHeight())
        #TotalWidgetHeight  = #TotalWidgetHeight + uiw!.getHeight()
        #updateUI()
    methodend

    method private void updateUI()
        #H=#MaxWidgetHeight
        #CwCanvas!.setSize(#W,#H)
        
        cols = #Cols
        maxLocalWidth = 0
        
        REM first discover the optimal distribution and column count
        while 1
            avg = #TotalWidgetHeight / cols
            
            cols! = new BBjVector()
            col! = new BBjVector()
            cols!.addItem(col!)
            it! = #WidgetList!.iterator()
            colHeight=0
            totalWidth=0
            maxColHeight=0
            while it!.hasNext()
                uiw! = it!.next()

                if abs(avg-colHeight) < abs(avg-(colHeight+uiw!.getHeight())) and cols!.size()<cols and col!.size()>0 then
                    col! = new BBjVector()
                    cols!.addItem(col!)
                    totalWidth=totalWidth+maxLocalWidth
                    maxLocalWidth=0
                    colHeight=0
                fi
                
                col!.addItem(uiw!)
                maxLocalWidth = max(maxLocalWidth,uiw!.getWidth())
                colHeight=colHeight+uiw!.getHeight()
                maxColHeight=max(maxColHeight,colHeight)
            wend
            totalWidth=totalWidth+maxLocalWidth
            
            if totalWidth > #W and cols>1 then
                cols=cols-1
                continue
            fi 
            
            #H = maxColHeight
            #CwCanvas!.setSize(#W,#H)

            break
        wend

        if cols <> #CurCols then
            REM now render that stuff, but only if something changed
            #CurCols=cols
            it! = cols!.iterator()
            x=0
            while it!.hasNext()
                col! = it!.next()
                it1! = col!.iterator()
                y=0
                maxW = 0
                while it1!.hasNext()
                    uiw! = it1!.next()
                    uiw!.setLocation(x,y)
                    y=y+uiw!.getHeight()
                    maxW = max(maxW,uiw!.getWidth())
                wend
                x=x+maxW
            wend
        fi
        
        
    methodend

classend


setesc byebye

sg!=BBjAPI().openSysGui("X0")
wnd! = sg!.addWindow(0,0,1024,768,"TEST")
wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!.setCallback(BBjAPI.ON_RESIZE,"resize")


f! = new ResponsiveFormFrame(wnd!,100,0,0,1024,10)

for i=1 to 10
    w!=new TestWidget(f!.getWnd(),100+i)
    f!.addWidget(w!)
next


process_events

resize:
    ev!=BBjAPI().getLastEvent()
    w=ev!.getWidth()
    h=ev!.getHeight()
    f!.setSize(w,h)
return

byebye:bye