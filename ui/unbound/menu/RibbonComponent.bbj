use ::components/ui/unbound/menu/menu.bbj::Menu
use ::components/ui/unbound/menu/menu.bbj::MenuItem

use ::components/ui/laf/laffactory.bbj::laffactory


rem ##################################################################################################
rem class RibbonComponent
rem ##################################################################################################

class public RibbonComponent implements UIWidget

    field private BBjNumber X
    field private BBjNumber Y
    field private BBjNumber W
    field private BBjNumber H
    field private BBjChildWindow Frame!
    field private BBjTabCtrl TabControl!
    field private Menu Menu!


    method private RibbonComponent()
    methodend

    method public RibbonComponent(BBjWindow parentWnd!, BBjNumber id, BBjNumber x, BBjNumber y, BBjNumber w, BBjNumber h, Menu menu!)

        #X=x
        #Y=y
        #W=w
        #H=h
        #Menu! = menu!

        #Frame! = parentWnd!.addChildWindow( id, x, y, w, h, "", $00000800$, BBjAPI().getSysGui().getAvailableContext())
        #Frame!.setClientEdge(0)
        #Frame!.setOpaque(0)
        #TabControl! = #Frame!.addTabCtrl( 100, 0, 0, w, h)
        laffactory.getLaf().setStyle(#TabControl!,"RibbonLike")
        
        #build()

    methodend

    method public void setWidth(BBjNumber w)

        #W = w
        #Frame!.setSize(#W,#H)
        #TabControl!.setSize(#W,#H)

    methodend

    method private void build()

        rem todo: rebuild - destroy everything first

        buttonWidth = 70
        buttonHeight = 60
        buttonGap = 5

        level1! = #Menu!.getChildren(null())
        i = 0
        while i < level1!.size()

            node! = level1!.getItem(i)
            panel! = #Frame!.addChildWindow( 200 + i, 0, 0, #W, #H, "", $00000800$, BBjAPI().getSysGui().getAvailableContext())
            #TabControl!.addTab(node!.getCaption(), panel!)
 
            level2! = node!.getChildren()
            j = 0
            while j < level2!.size()
                node1! = level2!.getItem(j)
                x = buttonGap + j * (buttonGap + buttonWidth)
                if j = level2!.size() - 1 then 
                    x = #W - (buttonGap + buttonWidth)
                fi
                tb! = panel!.addToolButton(100 + j, x, 0, buttonWidth, buttonHeight, node1!.getCaption())
                laffactory.getLaf().setStyle(tb!,"Main")
                
                tb!.setImage(node1!.getImage())
                tb!.setText(node1!.getCaption())
                j = j + 1
            wend

            i = i + 1

        wend

    methodend

classend