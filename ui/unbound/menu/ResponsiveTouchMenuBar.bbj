use ::components/ui/unbound/menu/menu.bbj::MenuItem
use ::components/ui/unbound/menu/menu.bbj::Menu


class public ResponsiveTouchMenuBar implements BBjControl

    field private BBjNumber X!
    field private BBjNumber Y!
    field private BBjNumber W!
    field private BBjNumber H!
    field private BBjChildWindow Frame!
    field private Menu Menu!
    field private MenuItem Level!
    field private BBjVector ButtonList!=new BBjVector()
    field private BBjVector MenuItemList!=new BBjVector()


    method private ResponsiveTouchMenuBar()
    methodend

    method public ResponsiveTouchMenuBar(BBjWindow parentWnd!, BBjNumber id, BBjNumber x!, BBjNumber y!, BBjNumber w!, BBjNumber h!, Menu menu!)

        #X!=x!
        #Y!=y!
        #W!=w!
        #H!=h!
        #Menu! = menu!

        #Frame! = parentWnd!.addChildWindow( id, x!, y!, w!, h!, "", $00000800$, BBjAPI().getSysGui().getAvailableContext())
        #Frame!.setClientEdge(0)
        #Frame!.setOpaque(0)

        #updateUI()

    methodend

    method public void setSize(BBjNumber w!, BBjNumber h!)
        #W!=w!
        #H!=h!
        #Frame!.setSize(w!,h!)
        #updateUI()
    methodend

    method public void setLocation(BBjNumber x!, BBjNumber y!)
        #X!=x!
        #Y!=y!
        #Frame!.setLocation(x!,y!)
    methodend

    method public void updateUI()

        it! = #ButtonList!.iterator()
        while it!.hasNext()
            c! = it!.next()
            c!.destroy()
        wend

        #ButtonList!.clear()
        #MenuItemList!.clear()

        items! = #Menu!.getChildren( #Level! )

rem horizontal

        it! = items!.iterator()
        x=0
        h=#Frame!.getHeight()
        i=0

        count = items!.size()

        if #Level! <> null() then
            count=count+1
        fi
        if #Level! <> null() and #Level!.getParent().getParent()<> null() then
            count=count+1
        fi

REM         w=int(#Frame!.getWidth()/max(1,count))
        w=h
        if #Level! <> null() then
            caption$ = "Home"
            btn! = #Frame!.addButton(1000+i,x,0,w,h,caption$)
            btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onButtonPush")
            btn!.addStyle("menu")
            btn!.addStyle("homebutton")
            #ButtonList!.addItem(btn!)
            #MenuItemList!.addItem(null())
            x=x+w
            i=i+1
        fi

        if #Level! <> null() and #Level!.getParent().getParent()<> null() then
            caption$ = #Level!.getCaption()
            btn! = #Frame!.addButton(1000+i,x,0,w,h,caption$)
            btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onButtonPush")
            btn!.addStyle("menu")
            btn!.addStyle("levelupbutton")
            btn!.addStyle(caption$)
            #ButtonList!.addItem(btn!)
            #MenuItemList!.addItem(#Level!.getParent())
            x=x+w
            i=i+1
        fi


        while it!.hasNext()
            item! = it!.next()
            caption$ = item!.getCaption()
            print caption$
            btn! = #Frame!.addButton(1000+i,x,0,w,h,caption$)
            btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onButtonPush")
            btn!.addStyle("menu")
            btn!.addStyle("levelbutton")
            btn!.addStyle("ID"+str(item!.getNodeId()))
            #ButtonList!.addItem(btn!)
            #MenuItemList!.addItem(item!)
            x=x+w
            i=i+1
        wend

        #Frame!.setSize(x,#Frame!.getHeight())

    methodend

    method public void onButtonPush(BBjButtonPushEvent ev!)
        declare auto MenuItem item!
        id = ev!.getButton().getID()
        item! = #MenuItemList!.getItem(id-1000)
        if item! <> null() then
            c! = item!.getChildren()
        fi

        if item! = null() or (c! <> null() and c!.size()) then
            #Level! = item!
            #updateUI()
        else
            item!.doScall()
        fi
    methodend


classend



