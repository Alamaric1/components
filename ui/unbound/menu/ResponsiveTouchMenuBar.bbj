use ::components/ui/unbound/menu/menu.bbj::MenuItem
use ::components/ui/unbound/menu/menu.bbj::Menu


class public ResponsiveTouchMenuBar implements BBjControl

    field private BBjNumber X!
    field private BBjNumber Y!
    field private BBjNumber W!
    field private BBjNumber H!
    field private BBjChildWindow Frame!
    field private Menu Menu!
    field private MenuItem Level!
    field private BBjVector ButtonList!=new BBjVector()
    field private BBjVector MenuItemList!=new BBjVector()  


    method private ResponsiveTouchMenuBar()
    methodend

    method public ResponsiveTouchMenuBar(BBjWindow parentWnd!, BBjNumber id, BBjNumber x!, BBjNumber y!, BBjNumber w!, BBjNumber h!, Menu menu!)

        #X!=x!
        #Y!=y!
        #W!=w!
        #H!=h!
        #Menu! = menu!

        #Frame! = parentWnd!.addChildWindow( id, x!, y!, w!, h!, "", $00000800$, BBjAPI().getSysGui().getAvailableContext())
        #Frame!.setBackColor(BBjAPI().makeColor("RED"))
        #Frame!.setClientEdge(0)
        #Frame!.setOpaque(0)

        #updateUI()

    methodend
    
    method public void updateUI()
    
        it! = #ButtonList!.iterator()
        while it!.hasNext()
            c! = it!.next()
            c!.destroy()
        wend
        
        #ButtonList!.clear()
        #MenuItemList!.clear() 
    
        items! = #Menu!.getChildren( #Level! )
        
rem horizontal

        it! = items!.iterator()
        x=0
        h=#Frame!.getHeight()
        i=0
        
        count = items!.size()
        
        if #Level! <> null() then 
            count=count+1
        fi
        if #Level! <> null() and #Level!.getParent().getParent()<> null() then
            count=count+1
        fi
        
        w=int(#Frame!.getWidth()/count)
        if #Level! <> null() then
            caption$ = "Home"
            btn! = #Frame!.addButton(1000+i,x,0,w-10,h,caption$)
            btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onButtonPush")
            #ButtonList!.addItem(btn!)
            #MenuItemList!.addItem(null())
            x=x+w
            i=i+1        
        fi

        if #Level! <> null() and #Level!.getParent().getParent()<> null() then
            caption$ = #Level!.getCaption()
            btn! = #Frame!.addButton(1000+i,x,0,w-10,h,caption$)
            btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onButtonPush")
            #ButtonList!.addItem(btn!)
            #MenuItemList!.addItem(#Level!.getParent())
            x=x+w
            i=i+1        
        fi          
        
       
        while it!.hasNext()
            
            item! = it!.next() 
            caption$ = item!.getCaption()
            print caption$
            btn! = #Frame!.addButton(1000+i,x,0,w,h,caption$)
            btn!.setCallback(BBjAPI.ON_BUTTON_PUSH,#this!,"onButtonPush")
            #ButtonList!.addItem(btn!)
            #MenuItemList!.addItem(item!)
            x=x+w
            i=i+1
        wend
        
    methodend

    method public void onButtonPush(BBjButtonPushEvent ev!)
        declare auto MenuItem item! 
        id = ev!.getButton().getID()
        item! = #MenuItemList!.getItem(id-1000)
        if item! <> null() then
            c! = item!.getChildren()
        fi
        
        if item! = null() or (c! <> null() and c!.size()) then
            #Level! = item!
            #updateUI()
        else
            item!.doScall()
        fi
    methodend
    

classend



class public test
    method public void execute(Object payl!)
        a=msgbox(str(payl!))
    methodend
classend

declare Menu menu!
declare auto MenuItem item!

menu! = new Menu()
it! = menu!.addMenuItem(null(),"item 1","")

menu!.addMenuItem(it!,"item 1.1","",new test(), "execute", "Hello World!")
menu!.addMenuItem(it!,"item 1.2","")
menu!.addMenuItem(it!,"item 1.3","")
it1! = menu!.addMenuItem(it!,"item 1.4","")

menu!.addMenuItem(it1!,"item 1.4.1","")
menu!.addMenuItem(it1!,"item 1.4.2","")
menu!.addMenuItem(it1!,"item 1.4.3","")

menu!.addMenuItem(null(),"item 2","")
menu!.addMenuItem(null(),"item 3","")
menu!.addMenuItem(null(),"item 4","")
menu!.addMenuItem(null(),"item 5","")
menu!.addMenuItem(null(),"item 6","")

wnd! = BBjAPI().openSysGui("X0").addWindow(10,10,800,600,"TEST")
wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")

m! = new ResponsiveTouchMenuBar(wnd!,100,0,0,800,25,menu!) 


PROCESS_EVENTS

byebye:bye

