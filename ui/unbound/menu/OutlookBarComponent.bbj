
use ::components/ui/unbound/menu/menu.bbj::Menu
use ::components/ui/unbound/menu/menu.bbj::MenuItem
use ::components/ui/laf/laffactory.bbj::laffactory
laf! = laffactory.getLaf()

rem ##################################################################################################
rem class OutlookBarComponent
rem ##################################################################################################

class public OutlookBarComponent implements UIWidget


    field private BBjNumber X
    field private BBjNumber Y
    field private BBjNumber W
    field private BBjNumber H
    field private BBjChildWindow Frame!
    field private BBjVector Buttons!
    field private BBjVector SubButtons!
    field private BBjVector ChildFrames!
    field private Menu Menu!
    field private BBjNumber Selected = 0

    field private java.util.HashMap MenuItems! = new java.util.HashMap()


    method private OutlookBarComponent()
    methodend

    method public OutlookBarComponent(BBjWindow parentWnd!, BBjNumber id, BBjNumber x, BBjNumber y, BBjNumber w, BBjNumber h, Menu menu!)

        #X=x
        #Y=y
        #W=w
        #H=h
        #Menu! = menu!

        #Frame! = parentWnd!.addChildWindow(id, x, y, w, h, "", $00000800$, BBjAPI().getSysGui().getAvailableContext())
        laffactory.getLaf().prepareWindow(#Frame!)
        #Frame!.setClientEdge(0)
        #Frame!.setOpaque(0)
        #build()
        
    methodend

    method private void build()
        laf! = laffactory.getLaf()
        rem todo: rebuild - destroy everything first

        #Buttons! = new BBjVector()
        #SubButtons! = new BBjVector()
        #ChildFrames! = new BBjVector()

        subButtonIndex = 0

        level1! = #Menu!.getChildren(null())
        panelHeight = #H - level1!.size() * 25
        i = 0
        while i<level1!.size()

            node! = level1!.getItem(i)

            y = i * 25
            if i > #Selected then
                y = y + panelHeight
            fi

            declare BBjChildWindow pane!
            pane! = #Frame!.addChildWindow(200 + i, 0, (i + 1) * 25, #W, panelHeight, "", $00000800$, BBjAPI().getSysGui().getAvailableContext())
            pane!.setBackColor(new BBjColor(255, 255, 255))

            if i = #Selected then
                pane!.setVisible(1)
            else
                pane!.setVisible(0)
            fi

            button! = #Frame!.addToolButton(300 + i, 0, y, #W, 25, node!.getCaption())
            laf!.setStyle(button!,"Navigation")
            
            button!.setCallback(BBjAPI.ON_TOOL_BUTTON_PUSH, #this!, "onToolButtonPush")
            button!.setVisible(1)
            button!.setImage(node!.getImage())
            button!.setText(node!.getCaption())
            button!.setAlignment(button!.ALIGN_LEFT)

            #Buttons!.addItem(button!)
            #ChildFrames!.addItem(pane!)

            declare BBjTree tree!
            tree! = pane!.addTree(100,0,0,#W,panelHeight)
            tree!.setRootVisible(0)
            tree!.setRoot(0,"ROOT")
            tree!.setCallback(BBjAPI.ON_TREE_DOUBLE_CLICK,#this!,"onTreeDoubleClick")
            sublevel! = node!.getChildren()
            parentNodeId = 0
            
            #addChildNodes(tree!, parentNodeId, sublevel!)

REM             j=0
REM             while j<level2!.size()
REM                 node1! = level2!.getItem(j)
REM                 y=  j * 50
REM                 declare BBjToolButton tb!
REM                 tb! = pane!.addToolButton(100 + subButtonIndex, 0, y, #W, 50, node1!.getCaption())
REM                 laf!.setStyle(tb!,"Main")
REM                 #SubButtons!.addItem(tb!)
REM                 subButtonIndex = subButtonIndex + 1
REM                 tb!.setImage(node1!.getImage())
REM                 tb!.setText(node1!.getCaption())
REM                 tb!.setToggleable(1)
REM                 tb!.setCallback(BBjAPI.ON_TOOL_BUTTON_PUSH, #this!, "onSubButtonPush")
REM                 tb!.setHorizontalTextPosition(tb!.TRAILING)
REM                 rem we really have to first change to bottom before going back to center
REM                 tb!.setVerticalTextPosition(tb!.BOTTOM)
REM                 tb!.setVerticalTextPosition(tb!.CENTER)
REM                 tb!.setAlignment(tb!.ALIGN_LEFT)
REM                 #MenuItems!.put(tb!,node1!)
REM                 j=j+1
REM             wend

            i=i+1

        wend

    methodend

    method private BBjNumber addChildNodes(BBjTree tree!, BBjNumber parentNodeId, BBjVector itemList!)
   
        j=0,id=parentNodeId
        while j<itemList!.size()
       
            node! = itemList!.getItem(j)
            id = id + 1 ; rem (parentNodeId+1)*40 + j
            tree!.addNode(id,parentNodeId,node!.getCaption())
            #MenuItems!.put(str(tree!)+"::"+str(id),node!)
           
            id = #addChildNodes(tree!,id,node!.getChildren())
            j=j+1
 
        wend
 
        methodret id
        
    methodend

    method public void onToolButtonPush(BBjToolButtonPushEvent ev!)
        #setSelected(ev!.getControl().getID()-300)
    methodend

    method public void setSelected(BBjNumber i)

        p! = #ChildFrames!.getItem(#Selected)
        p!.setVisible(0)
        #Selected = i
        c=#Buttons!.size()
        for i=0 to c-1  
            y=i*25
            if i > #Selected then 
                y = #H - (c - i) * 25
            fi
            btn! = #Buttons!.getItem(i)
            btn!.setLocation(0, y)
        next
        p! = #ChildFrames!.getItem(#Selected)        
        p!.setVisible(1)

    methodend

    method public void onSubButtonPush(BBjToolButtonPushEvent ev!)

        index = ev!.getControl().getID() - 100
        count = #SubButtons!.size()
        for i = 0 to count - 1
            button! = #SubButtons!.getItem(i)
            if i = index then
                button!.setSelected(1)
            else
                button!.setSelected(0)
            fi
        next

        node! = #MenuItems!.get(ev!.getControl())
        node!.doScall()

    methodend
    
    method public void onTreeDoubleClick(BBjTreeMouseDoubleClickEvent ev!)
    
        tree! = ev!.getControl()
        id    = ev!.getNodeID()
        node! = #MenuItems!.get(str(tree!)+"::"+str(id))

        node!.doScall()
        
    
    methodend
    
    method public void setSize(BBjNumber w, BBjNumber h)

        #Frame!.setSize(w,h)
        
REM         #Buttons! 
REM         #SubButtons! 
REM         #ChildFrames! 
        level1! = #Menu!.getChildren(null())
        panelHeight = h - level1!.size() * 25

        if #W <> w then
        
            j=0
            while j<#Buttons!.size()
                btn! = #Buttons!.getItem(j)
                btn!.setSize(w,25)
                j=j+1
            wend
            
        fi

        j=0
        while j<#ChildFrames!.size()
            cf! = #ChildFrames!.getItem(j)
            cf!.setSize(w,panelHeight)
            cf!.getControl(100).setSize(w,panelHeight)
            j=j+1
        wend

         
        #W=w
        #H=h
        
        c=#Buttons!.size()
        for i=0 to c-1  
            y=i*25
            if i > #Selected then 
                y = #H - (c - i) * 25
            fi
            btn! = #Buttons!.getItem(i)
            btn!.setLocation(0, y)
        next
        p! = #ChildFrames!.getItem(#Selected)        
        p!.setVisible(1)        
        
    methodend

classend

