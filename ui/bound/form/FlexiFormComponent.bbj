rem package components.ui.bound.form
use javax.xml.parsers.DocumentBuilder
use javax.xml.parsers.DocumentBuilderFactory
use java.io.StringReader
use org.xml.sax.InputSource
use org.w3c.dom.Document
use org.w3c.dom.Element
use org.w3c.dom.Node
use org.w3c.dom.NodeList
use java.util.HashMap

use com.basiscomponents.db.DataRow
use com.basiscomponents.db.DataField

use ::components/bl/BusinessComponent.bbj::BusinessComponent
use ::components/bl/ValidationError.bbj::ValidationError


use ::components/ui/bound/UIComponent.bbj::UIComponent
use ::components/ui/bound/toolbar/NavigationToolbarComponent.bbj::NavigationToolbarComponent
use ::components/ui/bound/toolbar/FormEditToolbarComponent.bbj::FormEditToolbarComponent
use ::components/ui/bound/controls/ForeignKeyPicker.bbj::ForeignKeyPicker
use ::components/ui/unbound/ResponsiveFormFrame.bbj::ResponsiveFormFrame
use ::components/ui/layout/BBMigPane.bbj::BBMigPane

use ::translations/bbtranslator.bbj::BBTranslator


rem /**
rem * FlexiFormComponent
rem *
rem * 
rem * @version 1.0
rem */
class public FlexiFormComponent extends UIComponent implements BBjControl

    field private   BBjNumber IsFormLocked
    field protected BBjChildWindow FormWnd!
    field protected ResponsiveFormFrame FormFrame!
    field protected BBjChildWindow ToolbarWnd!

    field protected HashMap ControlsByBcClass! = new HashMap()
    field protected HashMap ControlsByFillBcClass! = new HashMap()

    field protected NavigationToolbarComponent NavigationToolbar!
    field protected FormEditToolbarComponent FormEditToolbar!

    field protected DataRow   Rec!
    field protected DataRow   EditedRec!

    field protected HashMap ForeignKeyPickerList! = new HashMap()

    field protected BBjVector WarningIcons! = new BBjVector()

    field private HashMap RestoreControllerFilter!

    field private BBjNumber Enabled=1
    field private BBjNumber Editable=1
    
    field protected BBjNumber SelectiveEditMode =0
    
    rem new flexiform variables
    
    field private HashMap ElementList!= new HashMap()
    field private HashMap BCList!     = new HashMap()
    field private BBjNumber NewW
    field private BBjNumber NewH

    rem /**
    rem * Constructor FlexiFormComponent
    rem * Creates the component by using a given existing BBjChildWindow
    rem * @param BBjWindow wnd!: child window that the component shall occupy
    rem */
    method public FlexiFormComponent(BBjChildWindow cw!)

        #super!(cw!)
        #initialize()

    methodend



    rem /**
    rem * Constructor FlexiFormComponent
    rem * Creates the component by using a given existing BBjWindow
    rem * @param BBjWindow wnd!: child window that the component shall occupy
    rem * @param BBjNumber id: Control ID for the BBjChildWindow, which is created on wnd!
    rem * @param BBjNumber x: x-coordinate
    rem * @param BBjNumber y: y-coordinate
    rem * @param BBjNumber w: width
    rem * @param BBjNumber h: height
    rem */
    method public FlexiFormComponent(BBjWindow wnd!, BBjNumber id, BBjNumber x, BBjNumber y, BBjNumber w, BBjNumber h)

        #super!(wnd!,id,x,y,w,h)
        #initialize()

    methodend


    rem /**
    rem * Method setChildWindow
    rem * load the child window from a resource file (.arc)
    rem * @param BBjString resFile$: the name of the resource file
    rem * @param BBjNumber childId: the child window ControlID in the resource file
    rem */
    method public void loadLayout(BBjString resFile$)


        use java.io.FileInputStream
  
        ch=unt
        open (ch)resFile$
        resFileName$=fid(ch)(9)
        close (ch)
        declare InputSource is!
        is! = new InputSource(new FileInputStream(resFileName$))

REM FIXME: the solution in place does not mangle utf-8 chars but is not 3 tier aware
REM         ch=unt
REM         open (ch)resFile$
REM         read record (ch,siz=1000000)xml$
REM         close (ch)
REM         is! = new InputSource(new String(xml$))

        #ControlsByBcClass! = new HashMap()
    
        dbf! = DocumentBuilderFactory.newInstance()
        db! = dbf!.newDocumentBuilder()
        doc! = db!.parse(is!)
        doc!.getDocumentElement().normalize()
        
        sg!=BBjAPI().getSysGui()

        declare BBjWindow wnd!
        wnd! = #getWnd()
        #FormWnd! = wnd!.addChildWindow( wnd!.getAvailableControlID(),0,0,100,100,"",$00010800$,BBjAPI().getSysGui().getAvailableContext())
                
        #FormFrame! = new ResponsiveFormFrame(#FormWnd!,100,0,0,#FormWnd!.getWidth(),#FormWnd!.getHeight())
        #FormFrame!.getWnd().addStyle("FormWnd")
        #FormWnd!.setVisible(0)

        rem iterate groups
        nodeLst! = doc!.getElementsByTagName("group")
        
        for group = 0  to  nodeLst!.getLength()-1
        
                fstNode! = nodeLst!.item(group)
            
                if fstNode!.getNodeType() = Node.ELEMENT_NODE then
                    PRINT "+Group ",group+1
                 
                    ? fstNode!.getNodeName()
                    fstElmnt! = CAST(Element,fstNode!)
        
                    maxCols$= #getNodeValue(fstNode!,"maxcols")
                    horizontalAlign=0
                    horizontalAlign = num(#getNodeValue(fstNode!,"horizontalalign"),err=*next)
        
                    f2! = new ResponsiveFormFrame(#FormFrame!.getWnd(),#FormFrame!.getWnd().getAvailableControlID(),0,301,1024,0)
                    f2!.setNumCols(num(maxCols$,err=*next))
                    f2!.setHorizontalAlign(horizontalAlign)
                    f2wnd! = f2!.getWnd()
                    
                    rem determine panes
                    paneElmntLst! = fstElmnt!.getElementsByTagName("panel")
                 
                    for pane = 0  to  paneElmntLst!.getLength()-1
                  
                        paneNode! = paneElmntLst!.item(pane)
                    
                        if paneNode!.getNodeType() = Node.ELEMENT_NODE then
                           
                        
                            layc$= #getNodeValue(paneNode!,"layoutconstraints")
                            colc$= #getNodeValue(paneNode!,"columnconstraints")
                            rowc$= #getNodeValue(paneNode!,"rowconstraints")
                            name$= #getNodeValue(paneNode!,"name")
                            width$= #getNodeValue(paneNode!,"width")
                            height$= #getNodeValue(paneNode!,"height")
                            
                            if width$="" then
                                width=100
                            else
                                width=num(width$,err=*next)
                            fi

                            if height$="" then
                                height=100
                            else
                                height=num(height$,err=*next)
                            fi


                            rem create pane
                            cwCanvas! = f2wnd!.addChildWindow(f2wnd!.getAvailableControlID(),0,0,width,height,"",$00010800$,BBjAPI().getSysGui().getAvailableContext())
                            Layout! = new BBMigPane(cwCanvas!,layc$,colc$,rowc$)
                            
        
                            declare BBjFont font!
                            font_size=10
                    
                            cwCanvas!.setUserData(new HashMap())
                            cast(HashMap, cwCanvas!.getUserData()).put("scale",1.0)
                            
                            font! = BBjAPI().getSysGui().makeFont("Dialog",font_size,BBjAPI().getSysGui().PLAIN)
                            font_height=iff(font!.getStyle()=BBjAPI().getSysGui().BOLD,int(font_size*1.1),font_size)
REM                             cwCanvas!.setFont(font!)
                            cast(HashMap, cwCanvas!.getUserData()).put("prefFontSize",font!.getSize())
                            
                    
                    
                            lwpad=20,lhpad=12,iwpad=12,ihpad=12; rem label width/height padding, input control width/height padding
                            
                            rem determine elements (aka ontrols or widgets)
                            elElmntLst! = paneNode!.getElementsByTagName("element")
                            temp$ = str(java.io.File.createTempFile("blabla", ".call"))
                            if elElmntLst!.getLength() then
                                for element = 0  to  elElmntLst!.getLength()-1
                              
                                    elNode! = elElmntLst!.item(element)
                                
                                    if elNode!.getNodeType() = Node.ELEMENT_NODE then
                                        
                                        name$=""
                                        name$= #getNodeValue(elNode!,"name")
                                        if name$="" then
                                            name$=str(java.util.UUID.randomUUID())
                                        fi
                                        
                                        
                                        if #ElementList!.containsKey(name$)
                                            name1$=name$
                                            namei=0
                                            while #ElementList!.containsKey(name$)
                                                namei=namei+1
                                                name$=name1$+str(namei)
                                            wend
                                        fi

                                        
                                        type$= #getNodeValue(elNode!,"type")
                                        text$= #getNodeValue(elNode!,"text")
                                        textkey$= #getNodeValue(elNode!,"textkey")
                                        mask$= #getNodeValue(elNode!,"mask")                                        
                                        bindClass$= #getNodeValue(elNode!,"bindClass")
                                        bindField$= #getNodeValue(elNode!,"bindField")

                                        fillClass$=""
                                        fillField$=""
                                        fillClass$= #getNodeValue(elNode!,"fillClass")
                                        fillField$= #getNodeValue(elNode!,"fillField")
                                        
                                        cssClass$= #getNodeValue(elNode!,"cssClass")
                                        
                                        
                                        length= num(#getNodeValue(elNode!,"length"),err=*next)
                                         
                                        layc$= #getNodeValue(elNode!,"cellconstraints")
                                        typet$=type$
                                        while pos("/"=typet$)
                                            typet$=typet$(pos("/"=typet$)+1)
                                        wend

                                          x$="el! = new ::"+type$+".bbj::"+typet$+"(cwCanvas!)"
                                          rem execute x$
                                          rem workaround execute
                                              
                                          ch=unt
                                          open (ch,mode="O_CREATE,O_TRUNC")temp$
                                          write (ch)"ENTER"+$0a$+x$+$0a$+"EXIT"+$0a$
                                          close (ch)
                                          call temp$
                          
                                          
                                          el!.setSize((length+1)*25,font_height+ihpad)
                                          if text$>"" then
                                              el!.setText(text$)
                                          fi
                                          
                                          if mask$>"" then
                                              el!.setMask(mask$)
                                          fi
                                          if textkey$>"" then
                                              el!.setTextKey(textkey$)
                                          fi                                        
                                          el!.setName(name$,err=*next)
                                          el!.setUserData(new HashMap())
  REM                                         el!.setFont(font!,err=*next)
                                          if length>0 then
                                                el!.setLength(length) 
                                          fi
                                          
                                          el!.setBindField(bindField$)
                                          el!.setBindClass(bindClass$)
                                          
                                          el!.setFillField(fillField$)
                                          el!.setFillClass(fillClass$)
                                          
                                          if font!<>null() then
                                              cast(HashMap, el!.getUserData()).put("prefFontSize",font!.getSize())
                                          fi
                                          Layout!.add( el!, layc$ )
                                          
                                          #ElementList!.put(name$,el!)
                                        
                                          if bindClass$>"" then
                                              v! = #ControlsByBcClass!.get(bindClass$)
                                              if v! = null() then
                                                  v!=new BBjVector()
                                                  #ControlsByBcClass!.put(bindClass$,v!)
                                              fi
                                              v!.addItem(el!)
                                          fi
                                        
                                        if fillClass$>"" then
                                            v! = #ControlsByFillBcClass!.get(fillClass$)
                                            if v! = null() then
                                                v!=new BBjVector()
                                                #ControlsByFillBcClass!.put(fillClass$,v!)
                                            fi
                                            v!.addItem(el!)
                                        fi                                        
                                        
                                        if cssClass$>"" then
                                            el!.setCssClass(cssClass$) 
                                        fi
                                        
                                        
                                        rem events?
                                        eventLst! = paneNode!.getElementsByTagName("event")
                            
                                        if eventLst!.getLength() then
                                            for event = 0  to  eventLst!.getLength()-1
                              
                                                eventNode! = eventLst!.item(event)
                                
                                                    if eventNode!.getNodeType() = Node.ELEMENT_NODE then
                                                        type$= #getNodeValue(elNode!,"type")
                                                        method$= #getNodeValue(elNode!,"method")
                                                        el!.setCallback(2,#this!,method$)
                                                    fi
                                             next
                                        fi
                                    
                                        
        
                                    fi
                                    
                              
                              next; rem element
                              
                          fi
                        erase temp$,err=*next              
                        Layout!.layoutChildren()
                        f2!.addWidget(Layout!)
                            
                        fi
                  next; rem pane
        
                  rem add group to form
                  #FormFrame!.addWidget(f2!)
            fi
            
        next; rem group

        rem #getLaf().prepareWindow(#FormWnd!)
        rem #getLaf().setStyle(#FormWnd!,"ShadowedPanel")
        
        #FormWnd!.setTrack(0)
        #FormWnd!.setSize(#getW(), #getH())
        #FormFrame!.setSize(#getW(), #getH())

        if #this!.getShowToolBar() then
            #ToolbarWnd!.setSize(wnd!.getWidth(), 25)
            #FormWnd!.setLocation(0,26)
            #setSize(#getW(), #getH()) 
        fi

        #enableControls()
        #FormWnd!.setVisible(1)

    methodend



    method public FormEditToolbarComponent getFormEditToolbar()
        methodret #FormEditToolbar!
    methodend
    
    method public NavigationToolbarComponent getNavigationToolbar()
        methodret #NavigationToolbar!
    methodend    

    method protected BBjControl getWidget(BBjString name$)
        c! = #ElementList!.get(name$,err=*next)
        methodret c!
    methodend

    rem /**
    rem * Method initialize:
    rem * initialize the form. Creates hte edit and navigation toolbar.
    rem */
    method protected void initialize()

            rem initialize the toolbar

                

                declare BBjWindow wnd!
                wnd! = #getWnd()
    
                #ToolbarWnd! = wnd!.addChildWindow(100,0,0,wnd!.getWidth(),0,"",$00010800$,BBjAPI().getSysGui().getAvailableContext())
                #getLaf().prepareWindow(#ToolbarWnd!)
                #getLaf().setStyle(#ToolbarWnd!, "ToolBarLike")
            if #getShowToolBar() then    
                #FormEditToolbar! = new FormEditToolbarComponent(#ToolbarWnd!,10,0,2,9*UIComponent.getToolbarHeight(),UIComponent.getToolbarHeight())
                if #this!.getBC()<>null() then 
                    #FormEditToolbar!.setBC(#this!.getBC())
                fi
    
                #NavigationToolbar! = new NavigationToolbarComponent(#ToolbarWnd!,11,8*UIComponent.getToolbarHeight(),2,250,UIComponent.getToolbarHeight())
                if #this!.getBC()<>null() then
                    #NavigationToolbar!.setBC(#this!.getBC())
                fi
            fi
            
            #setEditing(0)

    methodend

    method protected void setSelectiveEditMode(BBjNumber selective)
        #SelectiveEditMode= selective
        
        #enableControls()
    methodend

    rem /**
    rem * Method setShowToolBar:
    rem * set the visiblity of the tool bar
    rem * @param BBjNumber fShow: 1 = visible, 0 = invisible
    rem */
    method public void setShowToolBar(BBjNumber fShow)

        #super!.setShowToolBar(fShow)
        if fShow and #FormWnd! <> null() then
            #ToolbarWnd!.setSize(#getWnd().getWidth(), UIComponent.getToolbarHeight())
            #FormWnd!.setLocation(0,UIComponent.getToolbarHeight()+1)
            #setSize(#getW(), #getH())
        else
            rem TODO implement hide toolbar
        fi

    methodend


    rem /**
    rem * Method setShowStatusBar:
    rem * set the visiblity of the status bar
    rem * @param BBjNumber fShow: 1 = visible, 0 = invisible
    rem */
    method public void setShowStatusBar(BBjNumber fShow)

        #super!.setShowStatusBar(fShow)
        #super!.setStatusBar(#getParentWnd().addStatusBar(5))
        #super!.getStatusBar().setTextAt(0,"...")

    methodend


    rem /**
    rem * Method setShowMenuBar:
    rem * set the visiblity of the menu bar
    rem * @param BBjNumber fShow: 1 = visible, 0 = invisible
    rem */
    method public void setShowMenuBar(BBjNumber fShow)

        #super!.setShowMenuBar(fShow)

        if fShow then
            if #FormEditToolbar! = null() then
                #FormEditToolbar! = new FormEditToolbarComponent(#ToolbarWnd!,10,0,2,9*UIComponent.getToolbarHeight(),UIComponent.getToolbarHeight())
                if #this!.getBC()<>null() then 
                    #FormEditToolbar!.setBC(#this!.getBC())
                fi
            fi
            if #NavigationToolbar! = null() then
                #NavigationToolbar! = new NavigationToolbarComponent(#ToolbarWnd!,11,8*UIComponent.getToolbarHeight(),2,250,UIComponent.getToolbarHeight())
                if #this!.getBC()<>null() then
                    #NavigationToolbar!.setBC(#this!.getBC())
                fi
            fi
            
            editMenu! = #super!.getMenuBar().addMenu(-2,"&Edit")
            #FormEditToolbar!.setupMenu(editMenu!)

            navMenu! = #super!.getMenuBar().addMenu(-3,"&Record")
            if #NavigationToolbar! <> null() then
                #NavigationToolbar!.setupMenu(navMenu!)
            fi
        fi

    methodend


    rem /**
    rem * Method setSize:
    rem * set the size of the form component
    rem * @param BBjNumber width: the width
    rem * @param BBjNumber height: the height
    rem */
    method public void setSize(BBjNumber w!, BBjNumber h!)
        DECLARE BBjAPI api!
        api! = BBjAPI()
        api!.removeTimer("delayedSetSize",err=*next)
        #NewW=w!
        #NewH=h!
        api!.createTimer("delayedSetSize",0.4,#this!,"delayedSetSize")
    methodend
    
    method public void delayedSetSize(BBjTimerEvent ev!)
    

        BBjAPI().removeTimer("delayedSetSize",err=*next)
          

        if #ToolbarWnd! <> null() AND #this!.getShowToolBar() then
            #ToolbarWnd!.setSize(#NewW,UIComponent.getToolbarHeight())
            #FormWnd!.setSize(#NewW, #NewH-UIComponent.getToolbarHeight())
            #FormFrame!.setSize(#NewW, #NewH-UIComponent.getToolbarHeight())
        else
            #FormWnd!.setSize(#NewW, #NewH)
            #FormFrame!.setSize(#NewW, #NewH)
        fi


        #super!.setSize(#NewW,#NewH)


    methodend


    rem /**
    rem * Method setVisible:
    rem * set the visibility of the form component
    rem * @param BBjNumber visible: 1 = visible, 0 = invisible
    rem */
    method public void setVisible(BBjNumber v)

        #super!.setVisible(v)

    methodend

    rem /**
    rem * Method setBC:
    rem * set the main business component for the form
    rem * @param BusinessComponent bc!: the business component
    rem */
    method public void setBC(BusinessComponent bc!)

        #super!.setBC(bc!)

        if #NavigationToolbar! <> null() then
            #NavigationToolbar!.setBC(#this!.getBC())
        fi

        if #FormEditToolbar! <> null() then
            #FormEditToolbar!.setBC(#this!.getBC())
        fi


        rem determine class name
        cl$ = str(bc!)
        while pos("::"=cl$)
            cl$=cl$(pos("::"=cl$)+2)
        wend
        
        #bindBC(cl$,bc!)

        ks! = #ControlsByBcClass!.keySet()
        it! = ks!.iterator()
        while it!.hasNext()
            c$=it!.next()
            if c$(1,1)="(" and c$(len(c$),1)=")" then
                ctrllist! = #ControlsByBcClass!.get(c$)
                
                c$=c$(2,len(c$)-2)
                
                bc1! = bc!.getForeignKeyBC(c$)
                
                if bc1!<>null() and ctrllist!<>null() then
                    it1! = ctrllist!.iterator()
                    while it1!.hasNext()
                        ctrl! = it1!.next()
                        ctrl!.setBC(bc1!)
                    wend
                fi
            fi
        wend
        
        ks! = #ControlsByFillBcClass!.keySet()
        it! = ks!.iterator()
        while it!.hasNext()
            c$=it!.next()
            if c$(1,1)="(" and c$(len(c$),1)=")" then
                ctrllist! = #ControlsByFillBcClass!.get(c$)
                
                c$=c$(2,len(c$)-2)
                
                bc1! = bc!.getForeignKeyBC(c$)
                
                if bc1!<>null() and ctrllist!<>null() then
                    it1! = ctrllist!.iterator()
                    while it1!.hasNext()
                        ctrl! = it1!.next()
                        ctrl!.setFillBC(bc1!)
                    wend
                fi
            fi
        wend        

    methodend

    method private void bindBC(BBjString bindClass$,BusinessComponent bc!)

            v! = #ControlsByBcClass!.get(bindClass$)
            if v! <> null()  then
                it! = v!.iterator()
                while it!.hasNext()
                  el! = it!.next()
                  el!.setBC(bc!)
                  #BCList!.put(bindClass$,bc!)
                wend
            fi
            
            fkmap! = bc!.getForeignKeyPointerList()
            it! = fkmap!.iterator()
            while it!.hasNext()
                it$=it!.next()
                bc1! = bc!.getForeignKeyBC(it$)
                cl$ = str(bc1!)
                while pos("::"=cl$)
                    cl$=cl$(pos("::"=cl$)+2)
                wend
                #bindBC(cl$,bc1!)
            wend
REM TODO: lazy loading!            
            
    
    methodend 


    rem /**
    rem * Method canSelectionChange:
    rem * This method is called from the business component when the selection changed
    rem * (needs overriding)
    rem * @param BBjNumber force: if set to 1, enforce saving unsaved data as selection will undeniably change
    rem * @return BBjNumber: 0 = can't change, 1 = change is OK
    rem */
    method public BBjNumber canSelectionChange(BBjNumber force)

        if  #EditedRec! = null() then
            methodret 1
        fi

        if force=0 then
            opt=35
        else
            opt=36
        fi

        a=msgbox(#super!.getTranslator().getTranslation("SAVE_CHANGES"),opt,#getBC().getTitle())

        switch a
            case 6
            rem yes
            #getBC().broadcastSaveRecord()
            canDrive=1
            #EditedRec! = null()
            break

            case 7
            rem no
            canDrive=1
            #EditedRec! = null()
            break

            case 2
            rem cancel
            canDrive=0
            break
        swend

        methodret canDrive

    methodend

    rem /**
    rem * Method setEditing:
    rem * invoked from BusinessComponent if a UI component started editing a record in the resultset
    rem * @param BBjNumber isEditing: -1 = i
    rem */
    method public void setEditing(BBjNumber isEditing)

        if isEditing=0 then
            #getWnd().focus()
            if #SelectiveEditMode then
                #setEnabled(0) 
            fi  
        else
            #setEnabled(1)
        fi
        

    methodend

    method public void setEnabled(BBjNumber enable!)
        if enable! <> #Enabled then
            #Enabled = enable!
            #enableControls()
        fi
        
    methodend

    method public void setEditable(BBjNumber editable!)
        if #Editable <> editable! then
            #Editable=editable!
            #enableControld()
        fi
    methodend


    method public void enableControls()

        ks! = #ElementList!.keySet()
        it!=ks!.iterator()
        e = #Editable AND #Enabled
        
        bc! = #getBC()
        if bc!<>null() then
            if #SelectiveEditMode and bc!.getEditingRecNo()=-1 then
                e=0
            fi
        else
            if #SelectiveEditMode then
                e=0
            fi 
        fi
        
        while it!.hasNext()
            k$ = it!.next()
            c!=#ElementList!.get(k$)
            ? 'SHOW',k$,"->",e
            c!.setEnabled(e)
        wend
        
    methodend
    
    rem /**
    rem * Method newRecord:
    rem * invoked from BusinessComponent, broadcast that the user wants to create a new record
    rem */
    method public void newRecord()

        #enableControls()
        
    methodend
    
    method private void markValidationErrors()
        
        ks! = #ElementList!.keySet()
        it! = ks!.iterator()
        while it!.hasNext()
            k$ = it!.next()
            e!=#ElementList!.get(k$)
            e!.markValidationErrors()
        wend
        
    methodend

    rem /**
    rem * Method saveRecord:
    rem * invoked from BusinessComponent, broadcast that the user wants to save the record
    rem * @return BBjNumber: 0 = record saved without errors, 1 = validation errors
    rem */
    method public BBjNumber saveRecord()


        DataRows!     = new HashMap()
        ks! = #BCList!.keySet()
        it! = ks!.iterator()
        while it!.hasNext()
            bclass$ = it!.next()
            bc! = #BCList!.get(bclass$)
            r! = new DataRow()
            DataRows!.put(bclass$,r!)
            
            widgets! = #ControlsByBcClass!.get(bclass$)
            it! = widgets!.iterator()
            while it!.hasNext()
                widget! = it!.next()
                if !widget!.willDoOwnSave() then
                    r! = widget!.prepareSave(r!)
                fi
            wend
            
            

            bc!.clearValidationErrors()
            val = bc!.validateRecord(bc!.getEditedRecord(),bc!.getEditedOriginalRecord())
            val = max(val,bc!.getFieldValidationErrorLevel())

            if val>0 then
                #markValidationErrors()

                if val=ValidationError.ERROR() then
                    a=msgbox(bc!.getValidationErrorMessage(),16,"validation failed")

                    methodret 1

                    rem aborting save and going back
                    rem todo: maybe nicer to collect info for all bc's and show them combined?
                else
                    if val=ValidationError.NOTICE() then
                        icon=64
                    fi

                    if val=ValidationError.WARN() then
                        icon=48
                    fi

                    a=msgbox(bc!.getValidationErrorMessage(),icon+1,"validation failed")

                    if a=2 then
                        methodret 1

                        rem "cancel - abort saving operation
                    fi
                fi
            fi            
           
        wend
            

        DataRows!     = new HashMap()
        ks! = #BCList!.keySet()
        it! = ks!.iterator()
        while it!.hasNext()
            bclass$ = it!.next()
            bc! = #BCList!.get(bclass$)
            r! = new DataRow()
            DataRows!.put(bclass$,r!)
            
            widgets! = #ControlsByBcClass!.get(bclass$)
            it! = widgets!.iterator()
            while it!.hasNext()
                widget! = it!.next()
                if !widget!.willDoOwnSave() then
                    r! = widget!.prepareSave(r!)
                fi
            wend
            
            bc!.saveRecord(bc!.getEditingRecNo(),r!)
            bc!.clearValidationErrors()
            bc!.clearFieldValidationErrors()
            
        wend
        
        #markValidationErrors()
        
        methodret 0

    methodend


    method private static BBjString getNodeValue(Node node!, BBjString tagName$)
    
                 paneElmnt! = CAST(Element,node!)

                 layElmntLst! = paneElmnt!.getElementsByTagName(tagName$)
                 layElmnt! = CAST(Element,layElmntLst!.item(0))
                 lay$=""
                 if layElmnt! <> null() then
                    lay! = layElmnt!.getChildNodes()
                    if lay!.item(0)<> null() then
                        lay$=lay!.item(0).getNodeValue()
                    fi
                 fi
                 
                 methodret lay$
                 
     methodend
     
     
    method public void destroy()
    
        #getBC().setEditedRecord(-1)

        if #NavigationToolbar! <> null() then
            #NavigationToolbar!.destroy()
        fi
      
        if #FormEditToolbar!<>null() then
            #FormEditToolbar!.destroy()
        fi
      
        
        ks! = #ForeignKeyPickerList!.keySet()
        it! = ks!.iterator() 
        while it!.hasNext()
            uic! = #ForeignKeyPickerList!.get(it!.next())
            uic!.destroy()
        wend
        #ForeignKeyPickerList!.clear()
        
        ks! = #ElementList!.keySet()
        it! = ks!.iterator()
        while it!.hasNext()
            k$ = it!.next()
            uic! = #ElementList!.get(k$)
            uic!.destroy()
        wend
        #ElementList!.clear()            

        ks! = #BCList!.keySet()
        it! = ks!.iterator()
        while it!.hasNext()
            k$=it!.next()
            bc! = #BCList!.get(k$)
            bc!.unregisterUI(#this!)
        wend

        
        #super!.destroy()
         
    
    methodend     

    rem /**
    rem * Method onSelectionChange:
    rem * This method is called from the business component when the selection changed
    rem */
    method public void onSelectionChange()
        
         #enableControls()
      
    methodend


classend
