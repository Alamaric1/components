use ::components/ui/bound/UIComponent.bbj::UIComponent
use ::components/ui/bound/controls/ForeignKeyPicker.bbj::ForeignKeyPicker

use ::components/ui/layout/BBMigPane.bbj::BBMigPane
use com.basiscomponents.db.DataRow
use com.basiscomponents.db.DataField
use ::components/bl/BusinessComponent.bbj::BusinessComponent

use ::i18n/CustomTranslator.bbj::CustomTranslator
use ::translations/bbtranslator.bbj::BBTranslator


use net.miginfocom.layout.PlatformDefaults

class public ControlWidget extends UIComponent 
 
    
    field protected BBjControl    Ctrl!
    field protected BBjNumber     Length
    field protected BBjNumber     Edited = 0 
    field protected BBjNumber     Focus = 0     
    
    field private BBjChildWindow WndCache!   
    
    field protected static BBjNumber charw = BBjAPI().getSysGui().getMeasure("W") +2
    
    field public static BBTranslator CustomTranslator!


    field protected BusinessComponent   FillBC!
    field protected BBjString           FillField$
    field protected BBjString           FillClass$

    field protected ForeignKeyPicker    FkPicker!
    
    field protected BBjToolButton       WarningIcon!
    
    field protected BBjNumber           Enabled=-1


    method protected ControlWidget(BBjChildWindow cwCanvas!)
        #super!(cwCanvas!)
        
        if #CustomTranslator! = null() then
             #CustomTranslator! = new CustomTranslator()
        fi
        
    methodend
    

    method protected void setCtrl (BBjControl ctrl!)

        #Ctrl!= ctrl!
        #Ctrl!.setCallback(BBjAPI.ON_GAINED_FOCUS,#this!,"onCtrlGainedFocus",err=*next)
        #Ctrl!.setCallback(BBjAPI.ON_LOST_FOCUS,#this!,"onCtrlLostFocus",err=*next)
        #Ctrl!.setCallback(BBjAPI.ON_EDIT_MODIFY,#this!,"onCtrlEditModify",err=*next)

REM PRIM KEY?
REM                     if #getBC().getPrimaryKeySegments().indexOf(ctrl_name$)>-1 and ctrl!.getControlType() <> 17 and ctrl!.getControlType() <> 19 then
REM                         ctrl!.setCallback(BBjAPI.ON_INPUT_KEYPRESS,#this!,"onCtrlInputKeypress",err=*next)
REM                     fi
        a! = #Ctrl!
REM         a!.setEditable(0,err=*next) 
REM         a!.setFocusable(0,err=*next)

        
    methodend
    
    method public void setLength(BBjNumber length)
        #Length=length
        #setSize(30+length*#charw,#getHeight())
    methodend 
    
    method public void setText(BBjString text!)
        x!= #getCtrl()
        x!.setText(text!)
    methodend
    
    method public void setMask(BBjString mask!)
        c! = #getCtrl()
        c!.setMask(mask$)
    methodend

    method public void setTextKey(BBjString textkey$)
        #setText( #CustomTranslator!.getTranslation(textkey$)  )
    methodend
    
    
    method public void setName(BBjString text$)
        x! = #getCtrl()
        x!.setName(text$)
    methodend    
 
     

     method public void setFont(BBjFont font!)
        #getCtrl().setFont(font!)
    methodend  
    
     method public void setBackColor(BBjColor col!)
        #getCtrl().setBackColor(col!)
    methodend      
           
    method public BBjNumber getWidth()
        methodret #getWnd().getWidth()
    methodend

    method public BBjNumber getHeight()
        methodret #getWnd().getHeight()
    methodend

    method public Boolean isVisible()
        methodret #getWnd().isVisible()
    methodend

    method public String getName()
        x!=#getCtrl()
        methodret x!.getName()
    methodend
    
    method public void setLocation(BBjNumber x!, BBjNumber y!)
        #getWnd().setLocation(x!,y!)
        
    methodend

    method public void setSize(BBjNumber w!, BBjNumber h!)
        dist=30
        if #FkPicker!<>null()  and #getCtrl().getControlType() <> BBjControl.LISTBUTTON_TYPE and #getCtrl().getControlType() <> BBjControl.LISTBOX_TYPE then
            dist=100
        fi
        #getWnd().setSize(w!,h!)
         
        #getCtrl().setSize(w!-dist,h!) 
        
        if #FkPicker! <> null() then
            #FkPicker!.relocate()
        fi
        
    methodend


    rem /**
    rem * Method onSelectionChange:
    rem * This method is called from the business component when the selection changed
    rem */
    method public void onSelectionChange()

          #clearValidationErrors()
          #displayData()

    methodend
    
    rem override for widgets like Date or so
   method public void displayData()

            if pos(".label"=#getName())=0 then
                if #getBindField() >"" then
                
                    if #getBC().getSelection().size()=1 then
                        #getBC().prepareResultSet(sel,sel)
                         
                        sel=#getBC().getSelection().getItem(i)
                        r! = cast(DataRow,#getBC().getResultSet().getItem(sel))

                        text$     = r!.getFieldAsString(#getBindField(),err=*next)
                        
                        x!=#Ctrl!
                        #setText(cvs(text$,2))
                    else
                        x!=#Ctrl!
                        #setText("")
                    fi
        
                    #Edited = 0
                fi
             fi
             
    methodend    


   method public void onCtrlGainedFocus(BBjGainedFocusEvent ev!)

        if #getBC()<>null() and #getBC().getSelection().size() then
            recno=#getBC().getSelection().getItem(0)
            #getBC().setEditedRecord(recno)

        fi

        #Edited = 1
        #Focus = 1

REM         if #EditedRec! = null() and #Rec! <> null() then
REM             #EditedRec! = cast(DataRow,#Rec!.clone())
REM         fi
REM 
REM         if #EditedFields! = null() then
REM             #EditedFields! = new java.util.HashMap()
REM         fi
REM 
REM         #EditedFields!.put(ev!.getControl().getName(),null())
REM         rem put null() to mark that the field has gained the focus. lostFocus will then asynchronously collect the data

    methodend


REM     method public void onCtrlInputKeypress(BBjInputKeypressEvent ev!)
REM 
REM         if ev!.getControl().getText()>"" and (ev!.getKeyCode() = 13 or ev!.getKeyCode() = 9) then
REM             #searchRecord()
REM         fi
REM 
REM     methodend


    method public void onCtrlLostFocus(BBjLostFocusEvent ev!)

        if #getBC()<>null() then
            #Focus = 0
            edrow! = #getBC().getEditedRecord()
            n$=#getName()
            v$=ev!.getText()
            edrow!.setFieldValue(n$,v$)
            validate = #getBC().validateField(n$,v$,#getBC().getEditedOriginalRecord(), edrow!)
            #markValidationErrors()
        fi

    methodend
    
    method protected void clearValidationErrors()

        if #WarningIcon!<> null() then
            #WarningIcon!.destroy()
            #WarningIcon!=null()
        fi

    methodend
        
    method public void markValidationErrors()

        #clearValidationErrors()
        bc! = #getBC()
        if bc! = null() then
            methodret
        fi
        v_err!=#getBC().getValidationError(#getName())

        if v_err! <>null() then
            wnd! = #getWnd()
            ctrl! = #getCtrl()
            #WarningIcon! =cast(BBjToolButton, wnd!.addToolButton(wnd!.getAvailableControlID(),ctrl!.getX()+ctrl!.getWidth()-8, ctrl!.getY(),16,16,""))
            #WarningIcon!.setRaisedEdge(0)
            #WarningIcon!.setFocusable(0)
            #WarningIcon!.setOpaque(0)
    
            if v_err!.getLevel() = v_err!.NOTICE() then
                icon! = BBjAPI().getSysGui().getImageManager().loadImageFromBytes($89504E470D0A1A0A0000000D49484452000000100000001008060000001FF3FF61000002D54944415478DAB553EF4B536114B63F20E843DF54483286A894380DE7744C9766CCCDA0FDF2EA74776B6D9A8ABAC9C85F586A35B5942D613A1D38E6129989111AE69CB66ECE2F12A5444116ACB68FE2372178BAEFB5042BFAD68187E73DE7DCF39C73EF796F42C2FF3083C15044D3F42CADD34575B5B5DF7535358C56AB1DD65656E6FEB3D06432255ED7EB1DB77B7A10D9D8403C1EC7FEFE3EB6B6B610080460B3D9A056AB47150A05EF6F5DB36F188D78BAB080682C8EC99939B4DCB1C3D0D6056BDF203C3301BCFFF0115EAF174AA512AC48EA71019A76AF854288BCD986BAB91B9DD3CB68724C43D9D28386112FDABD4BD0B474633914C698CB850A996CF4A858A7D389EEF6F7E3CBB738A4E65BB0F819F4AF7F45595D078811267EB33704597D3BC2CC06CC6633CACBCB859C40AD56EB0BB1DD873CB3A08602B03EFB0445EF1444B48D0339931881C63E83CE87E3989C98405959998F13D05657473FEFEE425AD70D6A9C01ED7B8B9CCA56FC327226317AFA1DA8B130AE983A100C0651525212E504AA28EA20168B21B3A20E32C76BC81F6D42D0E040FAD5064E80B0DC19E1E2249FC13EB7B9B9094971F10127A0D168A23BDBDBE02B9A20EE5F8164F0152403619C93D6730284894FE2249F7DAD11ABEC0462B1F87002954AE57B3237074DEB3D64357A20EC5B83B0378433974D9C0061E293F885FA7154B7D9E176BB2112890EBF01BB5391D562C1FC520829320BB22C01E474BD00AFEA3E9225468E897FBEF931928B68CC2F06A1D7EB515050203A5A65855CEEF1F97CB0BBFC4812EB905AE3405AE30C326D8B48BBE9C7D9AA0748B964C0F0E42C5CEC3DC8CFCFF71CBB4852A934572693C1E974E2F96A18D5D6015C54B5E2B4A00A42AA0DC68E11ACAC33B0DBED6474E4E5E5FDF95FB07B4D2A2D2D7590F1FC7E3F229108767676C0300CA6A6A6405114E9EC608B137FAF3DC1E2248B0C02D6F40281E025FB8E7B858585100A857B7C3E9FE1F1787A369FFE13A748E10FB7FBCC1083CC06B40000000049454E44AE426082$)
            fi
    
            if v_err!.getLevel() = v_err!.WARN() then
                icon! = BBjAPI().getSysGui().getImageManager().loadImageFromBytes($89504E470D0A1A0A0000000D49484452000000100000001008060000001FF3FF61000002524944415478DAB5926F48536118C51F188544D08AB25199061298E9A2200A0291CCB24030D40872146139CA5552391A597D72A8F73AB52D32E775830C8CCA046350D64C0CFFA10C6FDA1C33D3894EDC660D579BAED3DD822812B3A0033F789F0FE79C878797E87F4AAB5DB7F2AE7AF5AA7F32D7B292248E8974D58660D726FD95B9E166FC728E8972D8078DB00F1AC031EB1DA16D961CC095C52A3B5EAA3035FC10135603BACC2AD4B11B8A9664AE61E3D7704C9CCB3B63C73BF371816C04837ED431D15EAE2446F2E7762681E969D3607AA4112A650E0A2E64C2C157C1D2A1114262758B9A8DA5895B3846EA0FF867D1FF2A1D6969696178F37E04E77CC22DE2E66BD86DB18BB427DEB774EAE1196F81EBFD216464A423F5400A26F883E04D720CF61951CB6C7FBCB0B944BCE3495D32F0F5336CED29087833912B3B8C3D9B56806F2298D4842FDE29343F90415F26DDF75B407531B5F4779FC7CC643D9C0312F89C7B7149B611A78F46C3C94B30DAB90CB6D757313264126E216DFBC5AC2BA2234D06B1B07A05BAEB23E0B646E2D3480C66DDC784D6B39873EF867F7A17EC6609E6035EBC6854C0C04A33BE7F9A2C12690BA9EFED9B1C8CF62A6035113E0E6F856F5C8A4AE6040A0B92C3EFD9B104382D627CE8D2C039D61ADAC2D6D09025A2CA7C3AD95CBD195E8711CF8B23C03F22589F89C2E4E5A6225F9EFA631E784A68AFDE8990DA4CA7848346C9A93C8F7A865A2FC363E3E019E6F0B382C16098853439C6E29E9A1CA4CC267DF91942983C82464EA83827A020545E245415106E5F21689502D708BAEB843B37046E114A95D4FB0D123F95B46841BEF10000000049454E44AE426082$)
            fi
    
            if v_err!.getLevel() = v_err!.ERROR() then
                icon! = BBjAPI().getSysGui().getImageManager().loadImageFromBytes($89504E470D0A1A0A0000000D49484452000000100000001008060000001FF3FF61000002524944415478DA75935D489A6114C7BDE872DEB4E8D3C80B239036E80324A2A0A8208218ECCA2B0982D2F44AD07B711BDEB8722186287ABB60B1B58CAC315738C5E6FCBA1ADB606BA8EDB511B2816E5EFC77CE6B91963DF0E3BCCF39FFF37FCEFB2591D42CAFD7AB74BBDD028106E43D1E4F9FE4B6E572B9941B1B1B423A9D46A552B941329904D5F3AC6BD8EC743AF38944025F8F8E703039890F131388131C79FF291402D7595767B2BABADAE77038F2C7C7C762736C7A1AE77373F83B3F8F7F0447DE739EEBAC23BD40544DEC767B3E1C0EE3F3E121C274DACF9919FC9E9DC59F1A78CF79AEB32E128980FABE8B06369B0D8542015B2323382141616A0A670DE03CD7B7C6C6502E97C17DA281C562412E974360701027E3E3F8D1806FA3A3F8A25289F59D8101F0B25AAD5583959515F02D385B5AF05EA140A4B7578CEF7A7AF0B6BB1B7B6D6D08B6B7E3A0B353BC7E2A95229BCDC268345E1944A351389A9BB1C7A28E0E04291EC86478430621B9BC0E676B2B04418056ABAD1AF0452C16C3636A7AD9D5853D6A0ADEC22BAA3F218362B1586790F5F97CD85C5FC7333AF50519ED90B096D704E71D345D6C7B1BA9540A3A9DEE5434D0EBF5F7C844609380D78B35126E127C1AC3533DA7FD1A4DF07177576C369BCD0583C170F5312D2D2D2997979705BFDF8F001959E9243B8D6AA787C6F1114D96DADF179B4D265381F5373E67B55A7D7F7171F1947E18643219944A25F17D5F128FC7F9C99FB1AED1BF7487900F0D0D3DD06834BF161616701DCA9F0F0F0F3F641D21BD6ED044DC25648482E8270609D545ECBFC8CB2E744D978DFF01179CEE65B0A7865F0000000049454E44AE426082$)
            fi
    
            #WarningIcon!.setImage(icon!)
            #WarningIcon!.setToolTipText(v_err!.getMessage())
        fi

    methodend
    

    method public void onCtrlEditModify(BBjEditModifyEvent ev!)

        text$=ev!.getText()
        #getBC().broadcastEdit(ev!.getControl().getName(),text$,err=*next)

    methodend

    rem /**
    rem * Method setEditing:
    rem * invoked from BusinessComponent if a UI component started editing a record in the resultset
    rem * @param BBjNumber isEditing: -1 = i
    rem */
    method public void setEditing(BBjNumber isEditing)
    
        if isEditing=0 and pos(".label"=#getName())=0 then
            x!=#Ctrl!
            #displayData()
            #Edited=0
        fi
    
    methodend

    rem /**
    rem * Method newRecord:
    rem * invoked from BusinessComponent, broadcast that the user wants to create a new record
    rem */
    method public void newRecord()
        #Edited=0 
        #clearValidationErrors()
    methodend
    
    rem /**
    rem * Method restoreEditedField:
    rem * invoked from BusinessComponent, broadcast that the user wants to restore the field currently being edited
    rem */
    method public void restoreEditedField()

        if #Focus then
            #clearValidationErrors()
            #displayData()
        fi
        
    methodend

    rem /**
    rem * Method isEdited:
    rem * @return BBjNumber isEdited: 1 if the widget contains unsaved alternations 
    rem */
    method public BBjNumber isEdited()
        methodret #Edited
    methodend
    
    rem /**
    rem * Method prepareSave:
    rem * add the field's unsaved contents to the DataRow object
    rem  * 
    rem  * override to handle special formats, like date!!!
    rem  * 
    rem * @param DataRow row!: the row to collect the changes
    rem * @return DataRow: the row with the changes being added
    rem */
    method public DataRow prepareSave(DataRow r!)
    
        if #Focus then
            edrow! = #getBC().getEditedRecord()
            n$=#getName()
            v$=#getCtrl().getText()
            edrow!.setFieldValue(n$,v$)
            validate = #getBC().validateField(n$,v$,#getBC().getEditedOriginalRecord(), edrow!)
            #markValidationErrors()
        fi
    
        if #isEdited() then
        
            if #getFkPicker() <> null() then
                t!=#getFkPicker().getSelectedForeignKeyValue()
                if t!<>null() then
                    r!.setFieldValue(#getBindField(),str(t!))
                else
                    r!.setFieldValue(#getBindField(),"")
                fi
            else
                r!.setFieldValue(#getBindField(),#Ctrl!.getText())
            fi
        fi
        
        methodret r!
        
    methodend  
    
    rem /**
    rem * Method refresh:
    rem * refresh the displayed data
    rem * @param BusinessComponent bc!: the business component
    rem */
    method public void refresh(BBjNumber resetFilter)
    
        #clearValidationErrors()
        #displayData()

    methodend    


    method public void setCssClass(BBjString class$)
        #super!.setCssClass(class$)
        #Ctrl!.addStyle(class$)
    methodend

    method public void setCallback(int eventtype!, CustomObject o!, String method!)
    
        #Ctrl!.setCallback(eventtype!,o!,method!)
        
    methodend

        rem /**
    rem * Method setBC:
    rem * set the business component to drive this UI component
    rem * @param BusinessComponent bc!: the business component
    rem */
    method public void setBC(BusinessComponent bc!)

        #super!.setBC(bc!)

        ctrl_name$=#getCtrl().getName()
        if pos(".label"=ctrl_name$)>0 then
            lbl$ = #getBC().getColumnAttributes().getFieldAttribute(ctrl_name$(1,len(ctrl_name$)-6),"LABEL",err=*endif)
            x!=#Ctrl!
            #setText(lbl$+":")
        fi
        
    methodend


rem ------------medthods for fill BC


    method public void setFillField(BBjString field$)
        #FillField$=field$
    methodend

    method public void setFillClass(BBjString class$)
        #FillClass$=class$
    methodend    

    rem /**
    rem * Method setFillBC:
    rem * set the business component to drive this UI component
    rem * @param BusinessComponent bc!: the business component
    rem */
    method public void setFillBC(BusinessComponent bc!)

        #FillBC! = bc!
        rem todo: introduce a proxy class for the BC so this thing can also register for the update / 
        rem refresh callbacks of the businesscomponent

        
        fk$=#getBC().getForeignKeyFkField(#getName())
        
        #FkPicker! = new ForeignKeyPicker(#getWnd(),#getWnd().getAvailableControlID(),#this!,#FillBC!,fk$)
        
        
        #setSize(#getWnd().getWidth(),#getWnd().getHeight())
        #FkPicker!.setShowLinkedRecord(0)

        #FkPicker!.setVisible(#Enabled)
        
    methodend

    method public void destroy()
        
        if #getBC()<>null() then
            #getBC().unregisterUI(#this!) 
        fi
        
        #getCtrl().destroy()
        if #FkPicker! <> null() then
            #FkPicker!.destroy()
        fi
        
    methodend
    
    
    method public void setEnabled(BBjNumber fEnabled!)
    
        
          c!=#getCtrl()
          c!.setEnabled(fEnabled!)
          
          if #FkPicker!<>null() then
            
             #FkPicker!.setVisible(fEnabled!)
          fi
          #Enabled = fEnabled!
          
        
        
    methodend
    
    method public BBjNumber getControlType()
        methodret -1
    methodend

    rem /**
    rem * Method performEnablements:
    rem * override in rendering UI (mostly widget) components to do the actual enablements 
    rem * @param DataRow resultingEditedRec!: the currently edited record
    rem */    
    method public void performEnablements(DataRow row!)
            
            n$=#getName()
            visible=1
            visible$=row!.getFieldAttribute(n$,"VISIBLE",err=*next)
            if visible$>"" then
                visible=num(visible$,err=*next)
            fi
            #setVisible(visible)

            enabled=1
            enabled$=row!.getFieldAttribute(n$,"ENABLED",err=*next)
            if enabled$>"" then
                enabled=num(enabled$,err=*next)
            fi
            
            if #getBC().getEditingRecNo()<>-2 then
                enabled = enabled AND (#getIsPrimaryKeyField()=0)
            fi 
            
            #setEnabled(enabled)
    
    methodend

REM method protected void enableControl(BBjNumber fEditing)
REM     
REM             x! = #Ctrl!
REM             escape 
REM             if #getBC().getPrimaryKeySegments().indexOf(#getBindField())>-1 then
REM                 #setEnabled(!fEditing AND #Enabled)
REM             else
REM                 #setEnabled(fEditing and #Enabled)
REM             fi
REM               
REM    methodend 

classend
