use ::components/ui/bound/controls/ControlWidget.bbj::ControlWidget

use com.basiscomponents.db.DataRow


class public  CheckboxWidget extends ControlWidget

    method public CheckboxWidget(BBjChildWindow cwCanvas!)
        #super!(cwCanvas!)
        cwWidget! = cwCanvas!.addChildWindow(cwCanvas!.getAvailableControlID(),0,0,1,1,"",$00010800$,BBjAPI().getSysGui().getAvailableContext())
        #setWnd(cwWidget!)
        ck! = cwWidget!.addCheckBox(100,0,0,10,10,"")
        #setCtrl(ck!)
    methodend


    method public void displayData()

        if #getBindField() >"" then
            if #getBC().getSelection().size()=1 then
                rem #getBC().prepareResultSet(0,0)
                sel=#getBC().getSelection().getItem(i)

                r! = cast(DataRow,#getBC().getResultSet().getItem(sel))
                text$ = r!.getFieldAsString(#getBindField(),err=*next)

                rem #ToDo setValue method for Widget specify handling in subclass.
                tmpVal!=BBjAPI().FALSE
                if num(text$,err=*ENDIF)<>0 then tmpVal!=BBjAPI().TRUE endif
                cast(BBjCheckBox,#getCtrl()).setSelected(tmpVal!)
            else
                cast(BBjCheckBox,#getCtrl()).setSelected(BBjAPI().FALSE)
            fi

            #setEdited(0)
        fi

    methodend


    method public DataRow prepareSave(DataRow r!)

        if #getFocus() then
            edrow! = #getBC().getEditedRecord()
            n$=#getName()
            
            tmpVal$=iff(cast(BBjCheckBox,#getCtrl()).isSelected(),"1","0")

            edrow!.setFieldValue(n$,tmpVal$)
            validate = #getBC().validateField(n$,v$,#getBC().getEditedOriginalRecord(), edrow!)
            #markValidationErrors()
        fi

        if #getEdited() then
            
            tmpVal$=iff(cast(BBjCheckBox,#getCtrl()).isSelected(),"1","0")
            r!.setFieldValue(#getBindField(),tmpVal$)
            
        fi

        methodret r!

    methodend
    
    rem /**
    rem  * Returns the state of the BBjCheckbox held by this Widget as string. 
    rem  * If the checkbox is checked then 1 is returned, 0 otherwise.
    rem  * 
    rem  * @return state - The state of the checkbox
    rem  */
    method public String getText()
        ctrl! = #super!.getCtrl()
        if(ctrl! <> null()) then
            if(ctrl!.isSelected()) then
                methodret "1"
            fi
        fi
        methodret "0"
    methodend

classend

