
use ::BBMigPane.bbj::BBMigPane
use net.miginfocom.layout.PlatformDefaults

class public jabberwalky
    field private BBjAPI    API! = BBjAPI()
    field private BBjNumber TRUE = BBjAPI().TRUE
    field private BBjNumber FALSE = BBjAPI().FALSE
    field private BBjNumber sysgui_ch
    field private BBjSysGui SysGui!

    field private BBjTopLevelWindow Window!
    field private BBMigPane Layout!
    field private BBjChildWindow cWindow!
    field private BBMigPane cLayout!
    
    field public BBjNumber ActiveResize
    field public BBjNumber ResizeEventWidth
    field public BBjNumber ResizeEventHeight

    method public jabberwalky()

        #sysgui_ch = unt
        open(#sysgui_ch)"X0"
        #SysGui! = #API!.getSysGui()
        
        rem test platforms (NOTE: setting this sticks til reset or JVM re-started)
        rem PlatformDefaults.setPlatform(PlatformDefaults.WINDOWS_XP)
        rem PlatformDefaults.setPlatform(PlatformDefaults.MAC_OSX)
        rem PlatformDefaults.setPlatform(PlatformDefaults.GNOME)
        print "Platform=",
        switch PlatformDefaults.getPlatform()
            case PlatformDefaults.WINDOWS_XP
                print "Windows",
                break
            case PlatformDefaults.MAC_OSX
                print "Mac OS X",
                break
            case PlatformDefaults.GNOME
                print "Gnome",
                break
            case default
                print "Unknown",
        swend
        print " (real os=",System.getProperty("os.name"),")"

        WinFlags$=$00000000$
        WinFlags$=IOR(WinFlags$,$00000001$); rem Allows the window to be resized.
        WinFlags$=IOR(WinFlags$,$00000002$); rem Includes a close box with the window.
        rem WinFlags$=IOR(WinFlags$,$00000004$); rem Causes a horizontal scroll bar to appear if the window contents are not scaled, or the window is resized so that the entire contents are not displayed.
        rem WinFlags$=IOR(WinFlags$,$00000008$); rem Causes a vertical scroll bar to appear if the window contents are not scaled, or the window is resized so that the entire contents are not displayed.
        WinFlags$=IOR(WinFlags$,$00000010$); rem Allows the window to be initially invisible.
        rem WinFlags$=IOR(WinFlags$,$00000020$); rem Allows the window to be initially disabled.
        WinFlags$=IOR(WinFlags$,$00000080$); rem Allows the window to be maximized or minimized.
        rem WinFlags$=IOR(WinFlags$,$00000100$); rem Sets the window to be initially minimized.
        rem WinFlags$=IOR(WinFlags$,$00000800$); rem Include menu bar. Creates a menu bar.
        rem WinFlags$=IOR(WinFlags$,$00001000$); rem Sets the window to be initially maximized.
        rem WinFlags$=IOR(WinFlags$,$00002000$); rem Prevents the separator line between the menu and client area of a window from being drawn.
        WinFlags$=IOR(WinFlags$,$00010000$); rem Activates keyboard navigation in the window. If this flag is specified, key pressed ("t") events for arrow keys and the tab key will not be reported.
        rem WinFlags$=IOR(WinFlags$,$00020000$); rem Always positions the window on top of other windows.
        rem WinFlags$=IOR(WinFlags$,$00040000$); rem Creates a border around the dialog.
        rem WinFlags$=IOR(WinFlags$,$00080000$); rem Sets the window to behave as a dialog.
        rem WinFlags$=IOR(WinFlags$,$00100000$); rem Automatically arranges all controls and child windows placed in the window to fit.
        WinFlags$=IOR(WinFlags$,$00400000$); rem Allows use of a custom color palette. This provides a more accurate display of colors in bitmapped images.
        WinFlags$=IOR(WinFlags$,$00800000$); rem Sets the <Enter> key to behave as a <Tab> key.
        rem WinFlags$=IOR(WinFlags$,$01000000$); rem Creates the window without a title bar.
        WinFlags$=IOR(WinFlags$,$04000000$); rem Sets the window to automatically manage SYSCOLOR events.
        rem WinFlags$=IOR(WinFlags$,$20000000$); rem Causes Visual PRO/5 to report the mouse's right button down event.
        rem WinFlags$=IOR(WinFlags$,$40000000$); rem Sets the window to be group-modal within an MDI group.
        #Window! = #SysGui!.addWindow(500,500,500,500,"BBMigPane Layout Test",WinFlags$)
        #Window!.setName("window")
        
        declare BBjFont font!
        font_size=12
        font! = #SysGui!.makeFont("Dialog",font_size,#SysGui!.PLAIN)
        #Window!.setFont(font!)
        font_height=iff(font!.getStyle()=#SysGui!.BOLD,int(font_size*1.1),font_size)
        
        #Layout! = new BBMigPane(#Window!, "insets dialog,fillx", "[align label]rel[grow,fill]unrel[align label]rel[grow,fill]", "")

        declare BBjVector tx!
        declare BBjVector tm!
        
        tx! = #API!.makeVector()
        tidFirstName = 0; tx!.add(tidFirstName, "First Name")
        tidSurname = 1; tx!.add(tidSurname, "Surname")
        tidAddress = 2; tx!.add(tidAddress, "Address")
        tidOk = 3; tx!.add(tidOk, "OK")
        tidCancel = 4; tx!.add(tidCancel, "Cancel")
        tidHelp = 5; tx!.add(tidHelp, "Help")
        tidHelp2 = 6; tx!.add(tidHelp2, "Second Help")
        tm! = #SysGui!.getMeasures(tx!)

        declare BBjColor bcolor!
        bcolor! = #API!.makeColor(#SysGui!.LTGRAY)

        lwpad=0,lhpad=12,iwpad=12,ihpad=12; rem label width/height padding, input control width/height padding
        
        declare BBjStaticText lFirstName!
        lFirstName! =  #Window!.addStaticText(#Window!.getAvailableControlID(),0,0,num(tm!.get(tidFirstName))+lwpad,font_height+lhpad,str(tx!.get(tidFirstName)))
        lFirstName!.setName("lFirstName")
        lFirstName!.setFont(font!)
        lFirstName!.setBackColor(bcolor!)
        #Layout!.add( lFirstName!, "" )
        
        declare BBjInputE iFirstName!
        iFirstName! = #Window!.addInputE(#Window!.getAvailableControlID(),0,0,100,font_height+ihpad,"")
        iFirstName!.setName("iFirstName")
        iFirstName!.setFont(font!)
        #Layout!.add( iFirstName!, "" )
        
        declare BBjStaticText lSurname!
        lSurname! = #Window!.addStaticText(#Window!.getAvailableControlID(),0,0,num(tm!.get(tidSurName))+lwpad,font_height+lhpad,str(tx!.get(tidSurName)))
        lSurname!.setName("lSurname")
        lSurname!.setFont(font!)
        lSurname!.setBackColor(bcolor!)
        #Layout!.add( lSurname!, "" )
        
        declare BBjInputE iSurname!
        iSurname! = #Window!.addInputE(#Window!.getAvailableControlID(),0,0,100,font_height+ihpad,"")
        iSurname!.setName("iSurname")
        iSurname!.setFont(font!)
        #Layout!.add( iSurname!, "wrap" )
        
        declare BBjStaticText lAddress!
        lAddress! = #Window!.addStaticText(#Window!.getAvailableControlID(),0,0,num(tm!.get(tidAddress))+lwpad,font_height+lhpad,str(tx!.get(tidAddress)))
        lAddress!.setName("lAddress")
        lAddress!.setFont(font!)
        lAddress!.setBackColor(bcolor!)
        #Layout!.add( lAddress!, "" )

        declare BBjInputE iAddress!
        iAddress! = #Window!.addInputE(#Window!.getAvailableControlID(),0,0,200,font_height+ihpad,"")
        iAddress!.setName("iAddress")
        iAddress!.setFont(font!)
        #Layout!.add( iAddress!, "spanx, wrap unrel:push" )

        rem child window (pane) for the buttons to test button tags
        cWinFlags$=$00000000$
        rem cWinFlags$=IOR(cWinFlags$,$00000004$); rem Includes a horizontal scroll bar with the child window. The scroll bar is only visible if this flag is set, 
        rem            the 'TRACK'(0) mnemonic is used to prevent image scaling, and either the child window or the virtual drawing surface is resized.
        rem cWinFlags$=IOR(cWinFlags$,$00000008$); rem Includes a vertical scroll bar with the child window. The scroll bar is only visible if this flag is set, 
        rem            the 'TRACK'(0) mnemonic is used to prevent image scaling, and either the child window or the virtual drawing surface is resized.
        rem cWinFlags$=IOR(cWinFlags$,$00000010$); rem Sets the child window to be initially invisible.
        rem cWinFlags$=IOR(cWinFlags$,$00000020$); rem Sets the child window to be initially disabled.
        cWinFlags$=IOR(cWinFlags$,$00000800$); rem Creates a child window without a border.
        cWinFlags$=IOR(cWinFlags$,$00010000$); rem Activates keyboard navigation in the child window. 
        rem            If this flag is specified, key pressed ("t") events for arrow keys and the tab key will not be reported.
        rem cWinFlags$=IOR(cWinFlags$,$00100000$); rem Automatically arranges all controls to fit within the child window.
        rem cWinFlags$=IOR(cWinFlags$,$00200000$); rem Attaches the child window to one side of the parent frame, by default to the top frame. 
        rem            It will always occupy the full extent of the parent window's edge (even if the parent is resized later), 
        rem            but the other dimension is determined by the width or height specified when the child was created. 
        rem            By default, docked child windows are attached to the top of their parent. This can be changed with the 'DOCK' mnemonic.
        rem cWinFlags$=IOR(cWinFlags$,$00800000$); rem Sets the <Enter> key to behave as a <Tab> key.
        rem cWinFlags$=IOR(cWinFlags$,$01000000$); rem Draws a recessed client edge around the child window.
        rem cWinFlags$=IOR(cWinFlags$,$02000000$); rem Draws a raised edge around the child window.
        #cWindow! = #Window!.addChildWindow(#Window!.getAvailableControlID(),0,70,#Window!.getWidth(),25,"",cWinFlags$,#SysGui!.getAvailableContext())
        #cWindow!.setName("cWindow")
        #cWindow!.setFont(font!)
        #cWindow!.setBackColor(bcolor!)
        
        #cLayout! = new BBMigPane(#cWindow!, "nogrid,fill", "[grow,fill]", "")
        
        bwpad=16,bhpad=16; rem button width/height padding
        
        declare BBjMenuButton bOk!
        bOk! = #cWindow!.addMenuButton(#cWindow!.getAvailableControlID(),0,0,num(tm!.get(tidOk))+bwpad,font_height+bhpad,str(tx!.get(tidOk)))
        bOk!.setName("bOk")
        bOk!.setFont(font!)
        #cLayout!.add( bOk!, "sg,tag ok" )

        declare BBjMenuButton bCancel!
        bCancel! = #cWindow!.addMenuButton(#cWindow!.getAvailableControlID(),0,0,num(tm!.get(tidCancel))+bwpad,font_height+bhpad,str(tx!.get(tidCancel)))
        bCancel!.setName("bCancel")
        bCancel!.setFont(font!)
        #cLayout!.add( bCancel!, "sg,tag cancel" )

        declare BBjMenuButton bHelp!
        bHelp! = #cWindow!.addMenuButton(#cWindow!.getAvailableControlID(),0,0,num(tm!.get(tidHelp))+bwpad,font_height+bhpad,str(tx!.get(tidHelp)))
        bHelp!.setName("bHelp")
        bHelp!.setFont(font!)
        #cLayout!.add( bHelp!, "sg,tag help" )

        declare BBjMenuButton bHelp2!
        bHelp2! = #cWindow!.addMenuButton(#cWindow!.getAvailableControlID(),0,0,num(tm!.get(tidHelp2))+bwpad,font_height+bhpad,str(tx!.get(tidHelp2)))
        bHelp2!.setName("bHelp2")
        bHelp2!.setFont(font!)
        #cLayout!.add( bHelp2!, "sg,tag help2" )

        #cLayout!.layoutChildren()
                
        #Layout!.add( #cWindow!, "south")
        #Layout!.layoutChildren()
       
        #show()
    methodend

    method public void show()
        #Window!.setCallback(#Window!.ON_CLOSE, #this!, "closeWindow")
        #Window!.setCallback(#Window!.ON_RESIZE, #this!, "windowResize")
        #Window!.setCallback(#Window!.ON_SCREEN_RESIZE, #this!, "screenResize")
        if info(3,6)<>"5" then #Window!.setCallback(#Window!.ON_MOUSE_SCROLL, #this!, "mouseScroll")
        #Window!.setVisible(#TRUE)
        #Window!.focus()        
        process_events
    methodend

    method public void closeWindow(BBjCloseEvent ev!)
        if #sysgui_ch then close(#sysgui_ch)
        release
    methodend
    
    method public void windowResize(BBjResizeEvent p_event!)
        if !(#ActiveResize)
            #ActiveResize = #TRUE
            #API!.createTimer("ResizeEventTimer", .2, #this!, "resizeTimerEvent")
        endif
        #ResizeEventWidth = p_event!.getWidth()
        #ResizeEventHeight = p_event!.getHeight()
    methodend

    method public void screenResize(BBjScreenResizeEvent p_event!)
        if !(#ActiveResize)
            #ActiveResize = #TRUE
            #API!.createTimer("ResizeEventTimer", .2, #this!, "resizeTimerEvent")
        endif
        #ResizeEventWidth = p_event!.getWidth()
        #ResizeEventHeight = p_event!.getHeight()
    methodend

    method public void resizeTimerEvent(BBjTimerEvent p_event!)
        #Layout!.layoutChildren()
        #cLayout!.layoutChildren()
        #API!.removeTimer("ResizeEventTimer",err=*next)
        #ActiveResize = #FALSE
    methodend

    method public void mouseScroll(BBjScrollWheelEvent p_event!)
        rem context:i(2),code:u(1),id:u(2),flags:u(1),x:u(2),y:u(2)
        rem dim event$:#SysGui!.stringForEventTemplate
        dim event$:tmpl(#sysgui_ch)
        event$ = p_event!.getEventString()
        isShiftDown = and(event.flags$,$01$)=$01$
        isCtrlDown = and(event.flags$,$02$)=$02$
        isCmdDown = and(event.flags$,$08$)=$08$
        isAltDown = and(event.flags$,$10$)=$10$
        rem getWheelRotation(): amount of the scroll wheel's movement
        zoom_step = p_event!.getWheelRotation(); rem usually 1, can be 2 if scrolled very fast
        rem getScrollDirection(): 0=Mouse scrolled down (zoom out), 16=Mouse scrolled up (zoom in)
        zoom_in = p_event!.getScrollDirection()
        if isCtrlDown or isCmdDown then
            rem current_context:i(2),available_context:u(2),active_context_count:u(2),scale_x:b,
            rem scale_y:b,units:c(1),screen_width:u(2),screen_height:u(2),screen_hres:u(2),
            rem screen_vres:u(2),display_type:u(1),devname:c(1*=0),devtype:c(1*=0),devmode:c(1*=0)
            dim sysgui_fin$:tmpl(#sysgui_ch,ind=0)
            sysgui_fin$ = fin(#sysgui_ch,ind=0)
            if zoom_in then
                zoom_factor = sysgui_fin.scale_x + (zoom_step / 10)
            else
                zoom_factor = sysgui_fin.scale_x - (zoom_step / 10)
            endif
            if zoom_factor > 0.25 then
                print(#sysgui_ch)'SCALE'(zoom_factor,zoom_factor)
                #resizeTimerEvent(null())
            endif
        endif
    methodend
    
classend


new jabberwalky()