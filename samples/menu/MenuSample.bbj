use ::components/ui/unbound/menu/menu.bbj::Menu
use ::components/ui/unbound/menu/menu.bbj::MenuItem
use ::components/ui/unbound/menu/OutlookBarComponent.bbj::OutlookBarComponent

bbjdir$= System.getProperty("basis.BBjHome")


myLauncher! = new MyLauncher()

m! = new Menu()

m1! = m!.addMenuItem(null(),"One",bbjdir$+"/demos/BUIShowcase/code/customermaintenance/new.png")
m11! = m!.addMenuItem(m1!,"One-1","")
m12! = m!.addMenuItem(m1!,"One-2","")
m12a! = m!.addMenuItem(m12!,"One-2a",bbjdir$+"/demos/BUIShowcase/code/customermaintenance/new.png")
m12b! = m!.addMenuItem(m12!,"One-2b","")

rem "give the menu a program to run immediately from the menu:
m13! = m!.addMenuItem(m1!,"_util","","_util",bbjdir$,"","")

rem add a menu item that invokes a method
m14! = m!.addMenuItem(m1!,"a callback","",myLauncher!,"onClick","M14")  

m2! = m!.addMenuItem(null(),"Two","")
m3! = m!.addMenuItem(null(),"Three","")
m4! = m!.addMenuItem(null(),"Four","")
m5! = m!.addMenuItem(null(),"Five","")


sg! = BBjAPI().openSysGui("X0")
wnd! = sg!.addWindow(10,10,300,600,"Menu Demo")

wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")

ob! = new OutlookBarComponent(wnd!, 100, 0,0,300,600,m!)

? 'HIDE'
process_events

byebye: 
    bye
    
    
class public MyLauncher

    method public void onClick(Object arg!)
    
        a=msgbox("Payload was: "+str(arg!),0,"Item clicked!")
    
    methodend

classend